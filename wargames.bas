10 LET CLS$=CHR$(27)+"c"
20 DIM GRADE$(2)
30 DIM PFX%(9)
40 DIM NUM%(9)
50 DIM PFXHIT%(9)
60 DIM NUMHIT%(9)
70 DIM PASNAME$(2)
80 LET GRADE$(1)="F"
90 LET GRADE$(2)="F"
100 DIM COMPUTERTGT$(2)
110 DIM HUMANTGT$(3)
120 DIM A(9)
130 LET HT%=0
140 LET COMPUTERTGT$(1)=""
150 LET COMPUTERTGT$(2)=""
160 LET HUMANTGT$(1)=""
170 LET HUMANTGT$(2)=""
180 LET COMPUTER$=""
190 LET HUMAN$=""
200 LET CINDENT%=0
210 LET HINDENT%=0
220 LET SF%=0
230 PRINT CHR$(27)+"E"+CHR$(27)+"H": PRINT CHR$(27)+"c"
240 PRINT "WARGAMES SIMULATOR"
250 PRINT "(C) ANDY GLENN 2020, ALL RIGHTS RESERVED"
260 PRINT
270 INPUT "SELECT DELAY FOR TERMINAL CHARACTERS: ",D%
280 INPUT "DO YOU WANT HELP ENABLED (Y/N): ",H$
290 IF H$="N" OR H$="n" THEN LET H$="N": ELSE: LET H$="Y"
300 PRINT: PRINT "SELECT SYSTEM": PRINT
310 PRINT "1. IMSAI 8080 (DEFAULT)
320 PRINT "2. AMSTRAD PCW"
330 PRINT: INPUT "SYSTEM: ",S$
340 IF S$="2" THEN LET CLS$=CHR$(27)+"E"+CHR$(27)+"H"
350 IF H$="Y" THEN LET F$="INTRO.TXT": GOSUB 6060: ELSE: GOTO 370
360 IF H$="Y" THEN LET F$="IMSAI.TXT": GOSUB 6060: ELSE: GOTO 370
370 PRINT CLS$
380 PRINT"62K CP/M VERS. 2.2 MCL030210-D-F8"
390 PRINT
400 INPUT"A>",COMMAND$
410 IF COMMAND$="CLS" OR COMMAND$="cls" THEN PRINT CLS$: GOTO 400
420 IF COMMAND$="BYE" OR COMMAND$="bye" THEN PRINT "System halted, bye.": END
430 IF COMMAND$="DIR" OR COMMAND$="dir" THEN PRINT "A: BYE     COM : CLS     COM : DIR     COM: MODEM   COM": PRINT "A: DIALER  COM": GOTO 400
440 IF COMMAND$="MODEM" OR COMMAND$="modem" OR COMMAND$="MODEM.COM" OR COMMAND$="modem.com" THEN GOTO 470
450 IF COMMAND$="DIALER" OR COMMAND$="dialer" OR COMMAND$="DIALER.COM" OR COMMAND$="dialer.com" THEN GOTO 1190
460 PRINT COMMAND$;"?": GOTO 400
470 FOR W%=1 TO D%*10: NEXT W%: PRINT CLS$
480 PRINT "CHECKING DISK DRIVE";:GOSUB 5450
490 PRINT "LOADING DISK";:GOSUB 5450
500 PRINT "CHECKING DISK";:GOSUB 5450
510 PRINT "READING PROGRAM FROM DISK";:GOSUB 5450
520 FOR W%=1 TO D%*25: NEXT W%: PRINT CLS$
530 PRINT "DIALING";:GOSUB 5450
540 PRINT "CONNECTING";:GOSUB 5450
550 FOR W%=1 TO D%*20: NEXT W%: PRINT CLS$
560 LET T$="PDP 11/270 PRB TIP #45                                                TTY 34/984 ": GOSUB 5460: PRINT
570 LET T$="WELCOME TO THE SEATTLE PUBLIC SCHOOL DISTRICT DATANET": GOSUB 5460: PRINT
580 FOR P%=1 TO 3
590 LET T$="PLEASE LOGON WITH USER PASSWORD: ": GOSUB 5460: INPUT "",RESPONSE$
600 IF RESPONSE$="PENCIL" OR RESPONSE$="pencil" THEN LET T$="PASSWORD VERIFIED": GOSUB 5460: PRINT: PRINT: GOTO 670
610 LET T$="INVALID PASSWORD": GOSUB 5460: PRINT: PRINT
620 NEXT P%
630 LET T$="TOO MANY INVALID PASSWORD ATTEMPTS": GOSUB 5460: PRINT
640 LET T$="--DISCONNECTED--": GOSUB 5460: INPUT "",RESPONSE$
650 IF H$="Y" AND GRADE$(1)<>"F" AND GRADE$(2)<>"F" THEN LET F$="GRADES.TXT": GOSUB 6060
660 GOTO 370
670 LET T$="PLEASE ENTER STUDENT NAME: ": GOSUB 5460: INPUT "",STUDENTNAME$
680 FOR A%=1 TO LEN(STUDENTNAME$): IF MID$(STUDENTNAME$,A%,8)="Lightman" OR MID$(STUDENTNAME$,A%,8)="LIGHTMAN" OR MID$(STUDENTNAME$,A%,8)="lightman" THEN LET STUDENTNAME$="Lightman, David L.":NEXT A%
690 IF STUDENTNAME$="Lightman, David L." THEN GOTO 740
700 FOR A%=1 TO LEN(STUDENTNAME$): IF MID$(STUDENTNAME$,A%,4)="Mack" OR MID$(STUDENTNAME$,A%,4)="MACK" OR MID$(STUDENTNAME$,A%,4)="mack" THEN LET STUDENTNAME$="Mack, Jennifer K.": NEXT A%
710 IF STUDENTNAME$="Mack, Jennifer K." THEN GOTO 890
720 IF STUDENTNAME$="Exit" OR STUDENTNAME$="EXIT" OR STUDENTNAME$="exit" THEN GOTO 640
730 LET T$="NO STUDENT RECORD FOUND": GOSUB 5460: PRINT: GOTO 670
740 LET STUDENT%=1
750 PRINT
760 LET T$="STUDENT RECORD: "+STUDENTNAME$: GOSUB 5460: PRINT
770 PRINT
780 LET T$="     CLASS #    COURSE TITLE         GRADE    TEACHER    PERIOD    ROOM": GOSUB 5460: PRINT
790 LET T$="   ______________________________________________________________________": GOSUB 5460: PRINT
800 PRINT
810 LET T$="      S-202     BIOLOGY 2              "+GRADE$(STUDENT%)+"      LIGGET       3       214": GOSUB 5460: PRINT
820 LET T$="      E-314     ENGLISH 11B            D      TURMAN       5       172": GOSUB 5460: PRINT
830 LET T$="      H-221     WORLD HISTORY 11B      C      DWYMER       2       108": GOSUB 5460: PRINT
840 LET T$="      M-106     TRIG 2                 B      DICKERSON    4       315": GOSUB 5460: PRINT
850 LET T$="      PE-02     PHYSICAL EDUCATION     C      COMSTOCK     1       GYM": GOSUB 5460: PRINT
860 LET T$="      M-122     CALCULUS 1             B      LOGAN        6       240": GOSUB 5460: PRINT
870 PRINT
880 GOTO 1030
890 LET STUDENT%=2
900 PRINT
910 LET T$="STUDENT RECORD: "+STUDENTNAME$: GOSUB 5460: PRINT
920 PRINT
930 LET T$="     CLASS #    COURSE TITLE         GRADE    TEACHER    PERIOD    ROOM": GOSUB 5460: PRINT
940 LET T$="   ______________________________________________________________________": GOSUB 5460: PRINT
950 PRINT
960 LET T$="      S-202     BIOLOGY 2              "+GRADE$(STUDENT%)+"      LIGGET       3       214": GOSUB 5460: PRINT
970 LET T$="      E-325     ENGLISH 11B            A      ROBINSON     1       114": GOSUB 5460: PRINT
980 LET T$="      H-221     WORLD HISTORY 11B      B      DWYER        2       108": GOSUB 5460: PRINT
990 LET T$="      M-104     GEOMETRY 2             D      HALQUIST     4       307": GOSUB 5460: PRINT
1000 LET T$="      B-107     ECONOMICS              D      MARKS        5       122": GOSUB 5460: PRINT
1010 LET T$="      PE-02     PHYSICAL EDUCATION     C      COMSTOCK     6       GYM": GOSUB 5460: PRINT
1020 PRINT
1030 LET T$="(E)DIT: ": GOSUB 5460: INPUT "",RESPONSE$
1040 IF RESPONSE$="E" OR RESPONSE$="e" OR RESPONSE$="edit" THEN GOTO 1060: ELSE : PRINT: GOTO 670
1050 PRINT
1060 LET TEMPGRADE$=GRADE$(STUDENT%)
1070 LET T$="CLASS: ": GOSUB 5460: INPUT "",CLASS$
1080 LET T$="GRADE: ": GOSUB 5460: INPUT "",GRADE$(STUDENT%)
1090 IF CLASS$="S-202" OR CLASS$="s-202" THEN GOTO 1110
1100 PRINT: LET T$="RECORD LOCKED": GOSUB 5460: LET GRADE$(STUDENT%)=TEMPGRADE$: GOTO 1020
1110 IF GRADE$(STUDENT%)="a" OR GRADE$(STUDENT%)="A" THEN LET GRADE$(STUDENT%)="A": PRINT: LET T$="RECORD UPDATED": GOSUB 5460: PRINT: GOTO 670
1120 IF GRADE$(STUDENT%)="b" OR GRADE$(STUDENT%)="B" THEN LET GRADE$(STUDENT%)="B": PRINT: LET T$="RECORD UPDATED": GOSUB 5460: PRINT: GOTO 670
1130 IF GRADE$(STUDENT%)="c" OR GRADE$(STUDENT%)="C" THEN LET GRADE$(STUDENT%)="C": PRINT: LET T$="RECORD UPDATED": GOSUB 5460: PRINT: GOTO 670
1140 IF GRADE$(STUDENT%)="d" OR GRADE$(STUDENT%)="D" THEN LET GRADE$(STUDENT%)="D": PRINT: LET T$="RECORD UPDATED": GOSUB 5460: PRINT: GOTO 670
1150 IF GRADE$(STUDENT%)="e" OR GRADE$(STUDENT%)="E" THEN LET GRADE$(STUDENT%)="E": PRINT: LET T$="RECORD UPDATED": GOSUB 5460: PRINT: GOTO 670
1160 IF GRADE$(STUDENT%)="f" OR GRADE$(STUDENT%)="F" THEN LET GRADE$(STUDENT%)="F": PRINT: LET T$="RECORD UPDATED": GOSUB 5460: PRINT: GOTO 670
1170 IF GRADE$(STUDENT%)="u" OR GRADE$(STUDENT%)="U" THEN LET GRADE$(STUDENT%)="U": PRINT: LET T$="RECORD UPDATED": GOSUB 5460: PRINT: GOTO 670
1180 PRINT: LET T$="RECORD LOCKED": GOSUB 5460: LET GRADE$(STUDENT%)=TEMPGRADE$: GOTO 1020
1190 LET HITS%=0
1200 LET PF1%=399
1210 LET PF2%=437
1220 LET PF3%=767
1230 LET PF4%=936
1240 DATA 437,1083
1250 DATA 936,1493
1260 DATA 399,2364
1270 DATA 437,2977
1280 DATA 767,3395
1290 DATA 399,3582
1300 DATA 936,3923
1310 DATA 767,7305
1320 DATA 437,8739
1330 RESTORE
1340 FOR A%=1 TO 9
1350 READ PFX%(A%)
1360 READ NUM%(A%)
1370 NEXT A%
1380 PRINT CLS$
1390 PRINT: PRINT "DIALER": PRINT
1400 INPUT "MENU - (S)CAN, (V)IEW OR E(X)IT: ",RESPONSE$
1410 PRINT
1420 IF RESPONSE$="S" OR RESPONSE$="s" OR RESPONSE$="SCAN" OR RESPONSE$="scan" THEN GOTO 1460
1430 IF RESPONSE$="V" OR RESPONSE$="v" OR RESPONSE$="VIEW" OR RESPONSE$="view" THEN GOTO 2360
1440 IF RESPONSE$="X" OR RESPONSE$="x" OR RESPONSE$="EXIT" OR RESPONSE$="exit" THEN GOTO 370
1450 GOTO 1380
1460 INPUT "HOW MANY NUMBERS FOR EACH PREFIX TO DIAL: ",ND%
1470 IF ND% > 9999 THEN PRINT "MAXIMUM IS 9999": GOTO 1460
1480 LET LN%=0
1490 LET HITS%=0
1500 LET PFXSET%=0
1510 FOR A% = 1 TO ND%
1520 IF LN%=0 THEN PRINT CLS$
1530 IF LN%=0 THEN PRINT "                    TO SCAN FOR CARRIER TONES, PLEASE LIST"
1540 IF LN%=0 THEN PRINT"                         DESIRED AREA CODES AND PREFIXES"
1550 IF LN%=0 THEN PRINT
1560 IF LN%=0 THEN PRINT "AREA                AREA                AREA                AREA"
1570 IF LN%=0 THEN PRINT "CODE PRFX NUMBER    CODE PRFX NUMBER    CODE PRFX NUMBER    CODE PRFX NUMBER"
1580 IF LN%=0 THEN PRINT "________________________________________________________________________________": PRINT
1590 IF LN%=0 AND A%=1 THEN PRINT "(311)";PF1%;"          (311)";PF2%;"          (311)";PF3%;"          (311)";PF4%
1600 IF PFXSET%=1 THEN GOTO 1730
1610 IF LN%=0 AND A%=1 THEN INPUT "(311) ",PF1T%
1620 IF LN%=0 AND A%=1 THEN INPUT "                    (311) ",PF2T%
1630 IF LN%=0 AND A%=1 THEN INPUT "                                        (311) ",PF3T%
1640 IF LN%=0 AND A%=1 THEN INPUT "                                                            (311) ",PF4T%
1650 IF LN%=0 AND A%=1 THEN PRINT
1660 IF LN%=0 AND A%=1 AND PF1T%>0 AND PF1T%<1000 THEN LET PF1%=PF1T%
1670 IF LN%=0 AND A%=1 AND PF2T%>0 AND PF2T%<1000 THEN LET PF2%=PF2T%
1680 IF LN%=0 AND A%=1 AND PF3T%>0 AND PF3T%<1000 THEN LET PF3%=PF3T%
1690 IF LN%=0 AND A%=1 AND PF4T%>0 AND PF4T%<1000 THEN LET PF4%=PF4T%
1700 IF PF1T%>999 OR PF2T%>999 OR PF3T%>999 OR PF4T%>999 THEN PRINT: INPUT "INVALID PREFIXES ENTERED - PRESS ENTER KEY TO CONTINUE",RESPONSE$: PRINT: GOTO 1480
1710 IF LN%=0 AND A%=1 THEN LET PFXSET%=1: GOTO 1520
1720 IF INKEY$="Q" OR INKEY$="q" THEN GOTO 2300
1730 IF A% > 0 AND A% < 10 THEN Z$="000"
1740 IF A% > 9 AND A% < 100 THEN Z$="00"
1750 IF A% > 99 AND A% < 1000 THEN Z$="0"
1760 IF A% > 999 THEN Z$=""
1770 PRINT "(311)";PF1%;Z$;A%;
1780 LET FD%=0
1790 LET HIT$="N"
1800 FOR N%=1 TO 9
1810 IF INKEY$="Q" OR INKEY$="q" THEN GOTO 2300
1820 IF PF1%=PFX%(N%) AND A%=NUM%(N%) THEN LET HIT$="Y"
1830 IF PF1%=PFX%(N%) AND A%=NUM%(N%) THEN LET FD%=N%
1840 NEXT N%
1850 IF HIT$="Y" THEN LET HITS%=HITS%+1
1860 IF HIT$="Y" THEN LET PFXHIT%(HITS%)=PFX%(FD%)
1870 IF HIT$="Y" THEN LET NUMHIT%(HITS%)=NUM%(FD%)
1880 IF HIT$="Y" THEN PRINT "*   ";: ELSE: PRINT "    ";
1890 LET FD%=0
1900 LET HIT$="N"
1910 PRINT "(311)";PF2%;Z$;A%;
1920 FOR N%=1 TO 9
1930 IF INKEY$="Q" OR INKEY$="q" THEN GOTO 2300
1940 IF PF2%=PFX%(N%) AND A%=NUM%(N%) THEN LET HIT$="Y"
1950 IF PF2%=PFX%(N%) AND A%=NUM%(N%) THEN LET FD%=N%
1960 NEXT N%
1970 IF HIT$="Y" THEN LET HITS%=HITS%+1
1980 IF HIT$="Y" THEN LET PFXHIT%(HITS%)=PFX%(FD%)
1990 IF HIT$="Y" THEN LET NUMHIT%(HITS%)=NUM%(FD%)
2000 IF HIT$="Y" THEN PRINT "*   ";: ELSE: PRINT "    ";
2010 LET FD%=0
2020 LET HIT$="N"
2030 PRINT "(311)";PF3%;Z$;A%;
2040 FOR N%=1 TO 9
2050 IF INKEY$="Q" OR INKEY$="q" THEN GOTO 2300
2060 IF PF3%=PFX%(N%) AND A%=NUM%(N%) THEN LET HIT$="Y"
2070 IF PF3%=PFX%(N%) AND A%=NUM%(N%) THEN LET FD%=N%
2080 NEXT N%
2090 IF HIT$="Y" THEN LET HITS%=HITS%+1
2100 IF HIT$="Y" THEN LET PFXHIT%(HITS%)=PFX%(FD%)
2110 IF HIT$="Y" THEN LET NUMHIT%(HITS%)=NUM%(FD%)
2120 IF HIT$="Y" THEN PRINT "*   ";: ELSE: PRINT "    ";
2130 LET FD%=0
2140 LET HIT$="N"
2150 PRINT "(311)";PF4%;Z$;A%;
2160 FOR N%=1 TO 9
2170 IF INKEY$="Q" OR INKEY$="q" THEN GOTO 2300
2180 IF PF4%=PFX%(N%) AND A%=NUM%(N%) THEN LET HIT$="Y"
2190 IF PF4%=PFX%(N%) AND A%=NUM%(N%) THEN LET FD%=N%
2200 NEXT N%
2210 IF HIT$="Y" THEN LET HITS%=HITS%+1
2220 IF HIT$="Y" THEN LET PFXHIT%(HITS%)=PFX%(FD%)
2230 IF HIT$="Y" THEN LET NUMHIT%(HITS%)=NUM%(FD%)
2240 IF HIT$="Y" THEN PRINT "*": ELSE: PRINT " "
2250 LET FD%=0
2260 LET HIT$="N"
2270 LET LN%=LN%+1
2280 IF LN%=15 THEN LET LN%=0
2290 NEXT A%
2300 IF HITS%>SF% THEN LET SF%=HITS%
2310 PRINT: INPUT "                      PRESS ENTER KEY TO RETURN TO MENU",RESPONSE$
2320 IF SF%=0 THEN GOTO 1380
2330 PRINT CLS$
2340 IF H$="Y" AND HITS%>0 THEN LET F$="DIALER.TXT": GOSUB 6060
2350 GOTO 1380
2360 PRINT CLS$
2370 PRINT "                  NUMBERS FOR WHICH CARRIER TONES WERE DETECTED:"
2380 PRINT
2390 IF SF%=0 THEN PRINT "                                NO SYSTEMS FOUND"
2400 IF SF%=0 THEN PRINT: PRINT "         NOTE: TYPE LOAD AND PRESS ENTER KEY TO RESTORE SCAN RESULTS"
2410 FOR A%=1 TO SF%
2420 IF NUMHIT%(A%) >0 AND NUMHIT%(A%) <10 THEN Z$="000"
2430 IF NUMHIT%(A%) >9 AND NUMHIT%(A%) <100 THEN Z$="00"
2440 IF NUMHIT%(A%) >99 AND NUMHIT%(A%) <1000 THEN Z$="0"
2450 IF NUMHIT%(A%) >999 THEN Z$=""
2460 PRINT "                              (";CHR$(A%+64);")  (311)";PFXHIT%(A%);Z$;NUMHIT%(A%)
2470 NEXT A%
2480 PRINT: PRINT: PRINT "                        PRESS ENTER KEY TO RETURN TO MENU"
2490 INPUT "                        OR SELECT SYSTEM: ",RESPONSE$
2500 IF RESPONSE$="LOAD" OR RESPONSE$="load" THEN LET SF%=9: RESTORE: FOR A%=1 TO SF%: READ PFXHIT%(A%):READ NUMHIT%(A%): NEXT A%: GOTO 2360
2510 IF SF%=0 THEN GOTO 2690
2520 IF RESPONSE$="A" OR RESPONSE$="a" THEN GOSUB 6220: GOTO 2360
2530 IF SF%=1 THEN GOTO 2690
2540 IF RESPONSE$="B" OR RESPONSE$="b" THEN GOSUB 6220: GOTO 2360
2550 IF SF%=2 THEN GOTO 2690
2560 IF RESPONSE$="C" OR RESPONSE$="c" THEN GOTO 2700
2570 IF SF%=3 THEN GOTO 2690
2580 IF RESPONSE$="D" OR RESPONSE$="d" THEN GOSUB 6220: GOTO 2360
2590 IF SF%=4 THEN GOTO 2690
2600 IF RESPONSE$="E" OR RESPONSE$="e" THEN GOSUB 6220: GOTO 2360
2610 IF SF%=5 THEN GOTO 2690
2620 IF RESPONSE$="F" OR RESPONSE$="f" THEN GOTO 2830
2630 IF SF%=6 THEN GOTO 2690
2640 IF RESPONSE$="G" OR RESPONSE$="g" THEN GOSUB 6220: GOTO 2360
2650 IF SF%=7 THEN GOTO 2690
2660 IF RESPONSE$="H" OR RESPONSE$="h" THEN GOSUB 6220: GOTO 2360
2670 IF SF%=8 THEN GOTO 2690
2680 IF RESPONSE$="I" OR RESPONSE$="i" THEN GOTO 3700
2690 GOTO 1380
2700 PRINT CLS$
2710 PRINT "DIALING";:GOSUB 5450: PRINT CLS$
2720 PRINT "CONNECTING";:GOSUB 5450: PRINT CLS$
2730 FOR W%=1 TO D%*20: NEXT W%: PRINT CLS$
2740 LET T$="TGS SYSTEM A-45 34:34:33                                              Y-1293.323 ": GOSUB 5460: PRINT
2750 LET T$="UNION MARINE BANK - SOUTHWEST REGIONAL DATA CENTER": GOSUB 5460: PRINT
2760 PRINT
2770 LET T$="LOGON    > ": GOSUB 5460: INPUT "",RESPONSE$
2780 LET T$="PASSWORD > ": GOSUB 5460: INPUT "",RESPONSE$
2790 PRINT
2800 LET T$="INVALID LOGON. CLOSING CONNECTION": GOSUB 5460: PRINT
2810 LET T$="--DISCONNECTED--": GOSUB 5460: INPUT "",RESPONSE$
2820 GOTO 2360
2830 PRINT CLS$
2840 PRINT "DIALING";:GOSUB 5450: PRINT CLS$
2850 PRINT "CONNECTING";:GOSUB 5450: PRINT CLS$
2860 FOR W%=1 TO D%*20: NEXT W%: PRINT CLS$
2870 LET T$="007:2312:435:936                                       PAN-AM RESERVATION SYSTEM": GOSUB 5460: PRINT
2880 LET T$="TERMINAL STATUS: ACTIVE": GOSUB 5460: PRINT
2890 PRINT
2900 LET T$="  1.  FLIGHT SCHEDULES": GOSUB 5460: PRINT
2910 LET T$="  2.  CREDIT CHECK": GOSUB 5460: PRINT
2920 LET T$="  3.  PASSENGER INFORMATION": GOSUB 5460: PRINT
2930 LET T$="  4.  SYSTEM STATUS FUNCTIONS": GOSUB 5460: PRINT
2940 LET T$="  5.  ALTERNATE ROUTINGS": GOSUB 5460: PRINT
2950 PRINT
2960 LET T$="MENU CHOICE: ": GOSUB 5460: INPUT "",RESPONSE$
2970 IF RESPONSE$="1" THEN GOTO 3660
2980 IF RESPONSE$="2" THEN GOTO 3670
2990 IF RESPONSE$="3" THEN GOTO 3050
3000 IF RESPONSE$="4" THEN GOTO 3680
3010 IF RESPONSE$="5" THEN GOTO 3690
3020 LET T$="CLOSING CONNECTION": GOSUB 5460: PRINT
3030 LET T$="--DISCONNECTED--": GOSUB 5460: INPUT "",RESPONSE$
3040 GOTO 2360
3050 PRINT CLS$
3060 LET T$="007:2312:435:936                                       PAN-AM RESERVATION SYSTEM": GOSUB 5460: PRINT
3070 PRINT
3080 LET T$="  1.  NEW RESERVATION": GOSUB 5460: PRINT
3090 LET T$="  2.  CHECK RESERVATION": GOSUB 5460: PRINT
3100 PRINT
3110 LET T$="MENU CHOICE: ": GOSUB 5460: INPUT "",RESPONSE$
3120 IF RESPONSE$="1" THEN GOTO 3150
3130 IF RESPONSE$="2" THEN GOTO 3400
3140 GOTO 2860
3150 PRINT CLS$
3160 LET T$="007:2312:435:936                                       PAN-AM RESERVATION SYSTEM": GOSUB 5460: PRINT
3170 LET T$="DESTINATION         : ": GOSUB 5460: INPUT "",DEST$
3180 LET T$="POINT OF DEPARTURE  : CHICAGO/O'HARE": GOSUB 5460: PRINT
3190 PRINT
3200 LET T$="009:4277:019:907                                        DATE OF ISSUE : 03/10/82": GOSUB 5460: PRINT
3210 PRINT
3220 LET T$="                                           DEPART DATE : 08/18/83    FLIGHT: 114": GOSUB 5460: PRINT
3230 PRINT
3240 LET T$="                            CARRIER:   FLIGHT:   CLASS:   DATE:   TIME:  STATUS:": GOSUB 5460: PRINT
3250 LET T$="                              PANAM       114       Q     18AUG  0815A    OK": GOSUB 5460: PRINT
3260 LET T$="FROM: CHICAGO/O'HARE": GOSUB 5460: PRINT 
3270 LET T$="  TO: "+DEST$: GOSUB 5460: PRINT
3280 PRINT
3290 LET T$="NUMBER OF PASSENGERS  : ": GOSUB 5460: INPUT "",NUMPAS%
3300 FOR PAS%=1 TO NUMPAS%
3310 LET T$="NAME OF PASSENGER #": GOSUB 5460: PRINT PAS%;: INPUT ": ",PASNAME$(PAS%)
3320 NEXT PAS%
3330 PRINT
3340 LET T$="FARE:  ": GOSUB 5460: PRINT USING "####.##";1164.1*NUMPAS%;
3350 LET T$="   GJATWA  SLM  TWA  GJA": GOSUB 5460: PRINT
3360 LET T$="TAX:   ": GOSUB 5460: PRINT USING "####.##";70.9*NUMPAS%
3370 LET T$="TOTAL: ": GOSUB 5460: PRINT USING "####.##";(1164.1+70.9)*NUMPAS%
3380 LET T$="REQUEST: SPEC DATA": GOSUB 5460: PRINT
3390 LET T$="                         PRESS 'ENTER' KEY TO RETURN TO MENU": GOSUB 5460: INPUT "",RESPONSE$: GOTO 3050
3400 PRINT CLS$
3410 LET T$="007:2312:435:936                                       PAN-AM RESERVATION SYSTEM": GOSUB 5460: PRINT
3420 LET T$="DESTINATION         : "+DEST$: GOSUB 5460: PRINT
3430 LET T$="POINT OF DEPARTURE  : CHICAGO/O'HARE": GOSUB 5460: PRINT
3440 PRINT
3450 LET T$="009:4277:019:907                                        DATE OF ISSUE : 03/10/82": GOSUB 5460: PRINT
3460 PRINT
3470 LET T$="                                           DEPART DATE : 08/18/83    FLIGHT: 114": GOSUB 5460: PRINT
3480 PRINT
3490 LET T$="                            CARRIER:   FLIGHT:   CLASS:   DATE:   TIME:  STATUS:": GOSUB 5460: PRINT
3500 LET T$="                              PANAM       114       Q     18AUG  0815A    OK": GOSUB 5460: PRINT
3510 LET T$="FROM: CHICAGO/O'HARE": GOSUB 5460: PRINT 
3520 LET T$="  TO: "+DEST$: GOSUB 5460: PRINT
3530 PRINT
3540 LET T$="NUMBER OF PASSENGERS   :": GOSUB 5460: PRINT NUMPAS%
3550 FOR PAS%=1 TO NUMPAS%
3560 LET T$="NAME OF PASSENGER #": GOSUB 5460: PRINT PAS%;" : ";
3570 LET T$=PASNAME$(PAS%): GOSUB 5460: PRINT
3580 NEXT PAS%
3590 PRINT
3600 LET T$="FARE:  ": GOSUB 5460: PRINT USING "####.##";1164.1*NUMPAS%;
3610 LET T$="   GJATWA  SLM  TWA  GJA": GOSUB 5460: PRINT
3620 LET T$="TAX:   ": GOSUB 5460: PRINT USING "####.##";70.9*NUMPAS%
3630 LET T$="TOTAL: ": GOSUB 5460: PRINT USING "####.##";(1164.1+70.9)*NUMPAS%
3640 LET T$="REQUEST: SPEC DATA": GOSUB 5460: PRINT
3650 LET T$="                         PRESS 'ENTER' KEY TO RETURN TO MENU": GOSUB 5460: INPUT "",RESPONSE$: GOTO 3050
3660 PRINT CLS$: LET T$="NOT YET IMPLEMENTED": GOSUB 5460: GOTO 2860
3670 PRINT CLS$: LET T$="NOT YET IMPLEMENTED": GOSUB 5460: GOTO 2860
3680 PRINT CLS$: LET T$="NOT YET IMPLEMENTED": GOSUB 5460: GOTO 2860
3690 PRINT CLS$: LET T$="NOT YET IMPLEMENTED": GOSUB 5460: GOTO 2860
3700 PRINT CLS$
3710 PRINT "DIALING";:GOSUB 5450
3720 PRINT "CONNECTING";:GOSUB 5450
3730 FOR W%=1 TO D%*20: NEXT W%: PRINT CLS$
3740 PRINT "                         #":FOR W%=1 TO D%*5: NEXT W%: PRINT CLS$
3750 PRINT: PRINT "                                           #":FOR W%=1 TO D%*5: NEXT W%: PRINT CLS$
3760 PRINT: PRINT: PRINT "                                                          #":FOR W%=1 TO D%*5: NEXT W%: PRINT CLS$
3770 PRINT: PRINT: PRINT
3780 LET LOGON$=""
3790 LET T$="LOGON: ": GOSUB 5460: INPUT "",LOGON$
3800 IF LOGON$="joshua" OR LOGON$="JOSHUA" THEN GOTO 3890
3810 IF LOGON$="help" OR LOGON$="HELP" THEN GOSUB 4670: GOTO 3780
3820 IF LOGON$="help logon" OR LOGON$="HELP LOGON" THEN GOSUB 4670: GOTO 3780
3830 IF LOGON$="help games" OR LOGON$="HELP GAMES" THEN GOSUB 4700: GOTO 3780
3840 IF LOGON$="list games" OR LOGON$="LIST GAMES" THEN GOSUB 4740: GOTO 3780
3850 PRINT "IDENTIFICAION NOT RECOGNIZED BY SYSTEM"
3860 PRINT "--CONNECTION TERMINATED--"
3870 INPUT "",RESPONSE$
3880 GOTO 2360
3890 PRINT CLS$
3900 PRINT "145          11456          11889          11893": FOR W%=1 TO D%: NEXT W%
3910 PRINT "PRT CON. 3.4.5. SECTRAN 9.4.3.          PORT STAT: SB-345": FOR W%=1 TO D%: NEXT W%
3920 PRINT: FOR W%=1 TO D%: NEXT W%: PRINT CLS$
3930 PRINT "(311) 655-7385": FOR W%=1 TO D%: NEXT W%
3940 PRINT: FOR W%=1 TO D%: NEXT W%: PRINT: FOR W%=1 TO D%: NEXT W%: PRINT CLS$: PRINT: FOR W%=1 TO D%: NEXT W%: PRINT: FOR W%=1 TO D%: NEXT W%: PRINT CLS$: PRINT: PRINT: FOR W%=1 TO D%: NEXT W%: PRINT CLS$
3950 PRINT "(311) 767-8739": FOR W%=1 TO D%: NEXT W%
3960 PRINT "(311) 936-2364": FOR W%=1 TO D%: NEXT W%: PRINT CLS$
3970 PRINT "PRT. STAT.                   CRT. DEF.": FOR W%=1 TO D%: NEXT W%
3980 PRINT "================================================"
3990 PRINT "FSKJJSJ: SUSJKJ: SUFJSL:          DKSJL: SKFJJ: SDKFJLJ": FOR W%=1 TO D%: NEXT W%
4000 PRINT "SYSPROC FUNCT READY          ALT NET READY": FOR W%=1 TO D%: NEXT W%
4010 PRINT "CPU AUTH RY-345-AX3     SYSCOMP STATUS: ALL PORTS ACTIVE": FOR W%=1 TO D%: NEXT W%
4020 PRINT "22/34534.90/3289               CVB-3904-39490": FOR W%=1 TO D%: NEXT W%
4030 PRINT "(311) 936-2384":FOR W%=1 TO D%: NEXT W%
4040 PRINT "(311) 936-3582": FOR W%=1 TO D%: NEXT W%: PRINT CLS$
4050 PRINT "22/34534.3209                  CVB-3904-39490": FOR W%=1 TO D%: NEXT W%
4060 PRINT "12934-AD-43KJ: CENTR PAK":  FOR W%=1 TO D%: NEXT W%
4070 PRINT "(311) 767-1083": FOR W%=1 TO D%: NEXT W%: PRINT CLS$
4080 PRINT "FLD CRS: 33.34.543     HPBS: 34/56/67/83     STATUS FLT  034/304": FOR W%=1 TO D%: NEXT W%: PRINT CLS$
4090 PRINT "1105-45-F6-B456          NOPR STATUS: TRAK OFF     PRON ACTIVE": FOR W%=1 TO D%: NEXT W%
4100 PRINT "(45:45:45  WER: 45/29/01 XCOMP: 43239582 YCOMP: 3492930D ZCOMP: 343906834": FOR W%=1 TO D%: NEXT W%
4110 PRINT "                                          SRON: 65=65/74/84/65/89": FOR W%=1 TO D%: NEXT W%: PRINT CLS$
4120 PRINT "-           PRT. STAT.                        CRY. DEF.": FOR W%=1 TO D%: NEXT W%
4130 PRINT "(311) 936-1582==============================================": FOR W%=1 TO D%: NEXT W%
4140 PRINT "                  3453                3594": FOR W%=1 TO D%: NEXT W%: PRINT CLS$
4150 PRINT "FLJ42   TK01   BM90   R601   6J82   FP03   ZWO::   JW89": FOR W%=1 TO D%: NEXT W%
4160 PRINT "DEF TRPCON: 43.45342.349": FOR W%=1 TO D%: NEXT W%
4170 PRINT "SYSPROC FUNCT READY                            ALT NET READY":FOR W%=1 TO D%: NEXT W%
4180 PRINT: FOR W=1 TO 250: NEXT W
4190 PRINT "CPU AUTH RY-345-AX3     SYSCOMP STATUS: ALL PORTS ACTIVE": FOR W%=1 TO D%: NEXT W%
4200 PRINT "(311) 936-2364": FOR W%=1 TO D%: NEXT W%
4210 PRINT "**********************************************************************": FOR W%=1 TO D%: NEXT W%
4220 PRINT "1105-45-F6-B456                 NOPR STATUS: TRAK OFF   PRON ACTIVE": PRINT CLS$: FOR W%=1 TO D%: NEXT W%
4230 PRINT CLS$
4240 LET T$="GREETINGS PROFESSOR FALKEN.": GOSUB 5460: PRINT: PRINT
4250 INPUT "",JOSHUA$
4260 IF JOSHUA$="chat" OR JOSHUA$="CHAT" THEN GOSUB 4380: GOTO 4250
4270 FOR A%=1 TO LEN(JOSHUA$)
4280 IF MID$(JOSHUA$,A%,5)="hello" OR MID$(JOSHUA$,A%,5)="HELLO" THEN GOSUB 4380
4290 NEXT A%
4300 IF JOSHUA$="help games" OR JOSHUA$="HELP GAMES" THEN GOSUB 4700: GOTO 4250
4310 IF JOSHUA$="list games" OR JOSHUA$="LIST GAMES" THEN GOSUB 4740: GOTO 4250
4320 IF JOSHUA$="help" OR JOSHUA$="HELP" THEN GOSUB 4670: GOTO 4250
4330 IF JOSHUA$="GLOBAL THERMONUCLEAR WAR" OR JOSHUA$="global thermonuclear war" THEN GOTO 4900
4340 IF JOSHUA$="TICTACTOE" OR JOSHUA$="tictactoe" THEN GOTO 6260
4350 IF JOSHUA$="exit" OR JOSHUA$="EXIT" THEN PRINT: LET T$="GOODBYE PROFESSOR FALKEN": GOSUB 5460: PRINT: PRINT: GOTO 3860
4360 IF JOSHUA$="" THEN GOTO 4250
4370 PRINT: PRINT JOSHUA$;"?": PRINT: GOTO 4250
4380 PRINT: LET T$="HOW ARE YOU FEELING TODAY?": GOSUB 5460: PRINT: PRINT
4390 INPUT "",JOSHUA$
4400 FOR A%=1 TO LEN(JOSHUA$)
4410 IF MID$(JOSHUA$,A%,4)="fine" OR MID$(JOSHUA$,A%,4)="FINE" THEN GOTO 4440
4420 NEXT A%
4430 GOTO 4660
4440 PRINT: LET T$="EXCELLENT. IT'S BEEN A LONG TIME. CAN YOU EXPLAIN THE REMOVAL OF YOUR USER": GOSUB 5460
4450 PRINT: LET T$="ACCOUNT ON 6/23/1973?": GOSUB 5460: PRINT: PRINT
4460 INPUT "",JOSHUA$
4470 FOR A%=1 TO LEN(JOSHUA$)
4480 IF MID$(JOSHUA$,A%,8)="mistakes" OR MID$(JOSHUA$,A%,8)="MISTAKES" THEN GOTO 4510
4490 NEXT A%
4500 GOTO 4660
4510 PRINT: LET T$="YES THEY DO. ": GOSUB 5460
4520 FOR W%=1 TO D%*20: NEXT W%
4530 LET T$="SHALL WE PLAY A GAME?": GOSUB 5460: PRINT: PRINT
4540 INPUT "",JOSHUA$
4550 FOR A%=1 TO LEN(JOSHUA$)
4560 IF MID$(JOSHUA$,A%,7)="nuclear" OR MID$(JOSHUA$,A%,7)="NUCLEAR" THEN GOTO 4590
4570 NEXT A%
4580 GOTO 4660
4590 PRINT: LET T$="WOULDN'T YOU PREFER A GOOD GAME OF CHESS?": GOSUB 5460: PRINT: PRINT
4600 INPUT "",JOSHUA$
4610 FOR A%=1 TO LEN(JOSHUA$)
4620 IF MID$(JOSHUA$,A%,5)="later" OR MID$(JOSHUA$,A%,5)="LATER" THEN GOTO 4650
4630 NEXT A%: 
4640 GOTO 4660
4650 LET JOSHUA$="": PRINT: LET T$="FINE": GOSUB 5460: PRINT: PRINT: FOR W%=1 TO D%*20: NEXT W%: GOTO 4900
4660 RETURN 
4670 PRINT: FOR W%=1 TO 250: NEXT W%
4680 LET T$="NO HELP AVAILABLE": GOSUB 5460: PRINT: PRINT
4690 RETURN
4700 PRINT: FOR W%=1 TO 250: NEXT W%
4710 LET T$="'GAMES' REFERS TO MODELS, SIMULATIONS, AND GAMES WHICH HAVE TACTICAL AND": GOSUB 5460: PRINT
4720 LET T$="STRATEGIC APPLICATIONS": GOSUB 5460: PRINT: PRINT
4730 RETURN 
4740 PRINT: FOR W%=1 TO 250: NEXT W%
4750 LET T$="BLACK JACK": GOSUB 5460: PRINT
4760 LET T$="GIN RUMMY": GOSUB 5460: PRINT
4770 LET T$="HEARTS": GOSUB 5460: PRINT
4780 LET T$="BRIDGE": GOSUB 5460: PRINT
4790 LET T$="CHESS": GOSUB 5460: PRINT
4800 LET T$="POKER": GOSUB 5460: PRINT
4810 LET T$="FIGHTER COMBAT": GOSUB 5460: PRINT
4820 LET T$="GUERRILLA ENGAGEMENT": GOSUB 5460: PRINT
4830 LET T$="DESERT WARFARE": GOSUB 5460: PRINT
4840 LET T$="AIR-TO-GROUND ACTIONS": GOSUB 5460: PRINT
4850 LET T$="THEATERWIDE TACTICAL WARFARE": GOSUB 5460: PRINT
4860 LET T$="THEATERWIDE BIOTOXIC AND CHEMICAL WARFARE": GOSUB 5460: PRINT
4870 PRINT: FOR W%=1 TO D%*10: NEXT W%
4880 LET T$="GLOBAL THERMONUCLEAR WAR": GOSUB 5460: PRINT: PRINT
4890 RETURN
4900 PRINT CLS$
4910 LET HT%=0
4920 LET COMPUTERTGT$(1)=""
4930 LET COMPUTERTGT$(2)=""
4940 LET HUMANTGT$(1)=""
4950 LET HUMANTGT$(2)=""
4960 LET COMPUTER$=""
4970 LET HUMAN$=""
4980 LET CINDENT%=0
4990 LET HINDENT%=0
5000 LET DESTROYED$=""
5010 LET T$="GLOBAL THERMONUCLEAR WAR": PRINT "                            ";: GOSUB 5460
5020 PRINT: PRINT: PRINT
5030 LET T$="WHICH SIDE DO YOU WANT (USA OR USSR): ": PRINT "                     ";: GOSUB 5460: INPUT "",HUMAN$
5040 IF HUMAN$="usa" OR HUMAN$="USA" THEN LET HUMAN$="USA": COMPUTER$="USSR": GOTO 5070
5050 IF HUMAN$="ussr" OR HUMAN$="USSR" THEN HUMAN$="USSR": COMPUTER$="USA": GOTO 5070
5060 GOTO 4900
5070 IF COMPUTER$="USA" THEN USA$="COMPUTER": USSR$="HUMAN"
5080 IF COMPUTER$="USSR" THEN USSR$="COMPUTER": USA$="HUMAN"
5090 IF COMPUTER$="USA" THEN LET COMPUTERTGT$(1)="MOSCOW": LET COMPUTERTGT$(2)="SAINT PETERSBURG": LET CINDENT%=41: LET HINDENT%=1
5100 IF COMPUTER$="USSR" THEN LET COMPUTERTGT$(1)="LAS VEGAS": LET COMPUTERTGT$(2)="SEATTLE": LET CINDENT%=1: LET HINDENT%=41
5110 IF HUMAN$="USA" THEN LET CINDENT%=1: LET HINDENT%=41
5120 IF HUMAN$="USSR" THEN LET CINDENT%=41: LET HINDENT%=1
5130 PRINT CLS$
5140 IF DESTROYED$="(DESTROYED)" THEN GOTO 5600
5150 LET T$="GLOBAL THERMONUCLEAR WAR": PRINT "                            ";: GOSUB 5460
5160 PRINT: PRINT
5170 PRINT "           PLAYER: ";USA$;"                         PLAYER: ";USSR$
5180 PRINT "      ___    ____             ____                   _______________"
5190 PRINT "     |   \__/    \_____      /    |              ___/               \"
5200 PRINT "     |                 \    /    /           ___/                    \______"
5210 PRINT "     |                  \__/    /           /                               \"
5220 PRINT "     |                           \        _/                                 |"
5230 PRINT "     |        UNITED STATES       |      /           SOVIET UNION       ____/"
5240 PRINT "      \                           /     /                          ____/"
5250 PRINT "        \                        /     |                          /"
5260 PRINT "          \________          __/       \          _____   /\_    /"
5270 PRINT "                   \__      /            \__    _/     \_/   \__/"
5280 PRINT "                      \____/                \__/"
5290 PRINT
5300 IF MID$(COMMAND$,1,6)="LAUNCH" OR MID$(COMMAND$,1,6)="launch" THEN GOSUB 5470: GOTO 5430
5310 PRINT "PRIMARY TARGETS                           PRIMARY TARGETS"
5320 FOR A%=1 TO 80: PRINT "-";: NEXT A%
5330 IF CINDENT%>HINDENT% THEN GOTO 5370
5340 PRINT COMPUTERTGT$(1)+" "+DESTROYED$;: PRINT TAB(HINDENT%),HUMANTGT$(1)+" "+DESTROYED$
5350 PRINT COMPUTERTGT$(2)+" "+DESTROYED$;: PRINT TAB(HINDENT%),HUMANTGT$(2)+" "+DESTROYED$
5360 GOTO 5390
5370 PRINT HUMANTGT$(1)+" "+DESTROYED$;: PRINT TAB(CINDENT%),COMPUTERTGT$(1)+" "+DESTROYED$
5380 PRINT HUMANTGT$(2)+" "+DESTROYED$;: PRINT TAB(CINDENT%),COMPUTERTGT$(2)+" "+DESTROYED$
5390 PRINT
5400 INPUT "COMMAND (TARGET, LAUNCH, EXIT): ",COMMAND$
5410 IF COMMAND$="exit" OR COMMAND$="EXIT" THEN PRINT CLS$: LET T$="A STRANGE GAME. ": GOSUB 5460: FOR W%=1 TO D%*25: NEXT W%: LET T$="THE ONLY WINNING MOVE IS NOT TO PLAY!": GOSUB 5460:PRINT: PRINT: GOTO 4250
5420 IF MID$(COMMAND$,1,6)="TARGET" OR MID$(COMMAND$,1,6)="target" THEN LET HT%=HT%+1: LET HUMANTGT$(HT%)=MID$(COMMAND$,8,LEN(COMMAND$))
5430 IF HT%>2 THEN PRINT "MAX TARGETS SELECTED": FOR W%=1 TO D%*50: NEXT W%: LET HT%=2: GOTO 5130
5440 PRINT CLS$: GOTO 5130
5450 FOR A%=1 TO 3: FOR W%=1 TO D%*5: NEXT W%: PRINT ".";:NEXT A%: FOR W%=1 TO D%*20: NEXT W%: PRINT CLS$: RETURN
5460 FOR A%=1 TO LEN(T$): PRINT MID$(T$,A%,1);:FOR W%=1 TO D%: NEXT W%: NEXT A%: FOR W%=1 TO D%: NEXT W%: RETURN
5470 FOR A%=1 TO 4: PRINT "TRAJECTORY HEADING  ";:NEXT A%
5480 FOR A%=1 TO 4: PRINT "------------------  ";:NEXT A%
5490 FOR T%=1 TO 4
5500 PRINT TAB(1);"A-5520-A";: PRINT TAB(11) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%: PRINT TAB(16) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%
5510 PRINT TAB(21);"B-5520-A";: PRINT TAB(31) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%: PRINT TAB(36) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%
5520 PRINT TAB(41);"C-5520-A";: PRINT TAB(51) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%: PRINT TAB(56) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%
5530 PRINT TAB(61);"D-5520-A";: PRINT TAB(71) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%: PRINT TAB(76) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%
5540 NEXT T%
5550 PRINT
5560 PRINT: INPUT "                          Press 'ENTER' key to CONTINUE",RESPONSE$
5570 LET HT%=0
5580 LET DESTROYED$="(DESTROYED)"
5590 LET COMMAND$="": RETURN
5600 REM **** WOPR CALLS YOU ****
5610 PRINT CLS$
5620 IF H$="Y" THEN LET F$="WOPR.TXT": GOSUB 6060
5630 LET T$="--CONNECTION TERMINATED--": GOSUB 5460: PRINT
5640 FOR W%=1 TO D%*100: NEXT W%
5650 PRINT CLS$
5660 FOR A%=1 TO 3: PRINT "INCOMING MODEM CARRIER": FOR W%=1 TO D%*25: NEXT W%: PRINT CLS$: FOR W%=1 TO D%*25: NEXT W%: NEXT A%
5670 PRINT "CONNECTING";:GOSUB 5450
5680 FOR W%=1 TO D%*100: NEXT W%
5690 PRINT CLS$
5700 LET T$="GREETINGS PROFESSOR FALKEN.": GOSUB 5460: PRINT: PRINT
5710 INPUT "",JOSHUA$
5720 FOR A%=1 TO LEN(JOSHUA$)
5730 IF MID$(JOSHUA$,A%,9)="incorrect" OR MID$(JOSHUA$,A%,9)="INCORRECT" THEN GOTO 5750: ELSE: PRINT: GOTO 4260
5740 NEXT A%
5750 PRINT: LET T$="I'M SORRY TO HEAR THAT, PROFESSOR.": GOSUB 5460: PRINT: PRINT
5760 LET T$="YESTERDAY'S GAME WAS INTERRUPTED.": GOSUB 5460: PRINT: PRINT
5770 LET T$="ALTHOUGH PRIMARY GOAL HAS NOT YET": GOSUB 5460: PRINT
5780 LET T$="BEEN ACHIEVED, SOLUTION IS NEAR.": GOSUB 5460: PRINT: PRINT
5790 INPUT "", JOSHUA$
5800 FOR A%=1 TO LEN(JOSHUA$)
5810 IF MID$(JOSHUA$,A%,4)="what" OR MID$(JOSHUA$,A%,4)="WHAT" THEN GOTO 5830: ELSE: PRINT: GOTO 4260
5820 NEXT A%
5830 PRINT: LET T$="YOU SHOULD KNOW PROFESSOR. ": GOSUB 5460: FOR W%=1 TO D%*20: NEXT W%: LET T$="YOU PROGRAMMED ME.": GOSUB 5460: PRINT: PRINT
5840 INPUT "",JOSHUA$
5850 FOR A%=1 TO LEN(JOSHUA$)
5860 IF MID$(JOSHUA$,A%,4)="what" OR MID$(JOSHUA$,A%,4)="WHAT" THEN GOTO 5880: ELSE: PRINT: GOTO 4260
5870 NEXT A%
5880 PRINT: LET T$="TO WIN THE GAME.": GOSUB 5460: PRINT: PRINT
5890 FOR W%=1 TO D%*200: NEXT W%: PRINT CLS$
5900 LET GTE%=0: LET ETR%=30
5910 FOR GC%=1 TO 30
5920 LET GTE%=GTE%+1: LET ETR%=ETR%-1
5930 PRINT CLS$
5940 FOR A%=1 TO 15: PRINT: NEXT A%
5950 FOR A%=1 TO 80: PRINT "_";: NEXT A%
5960 PRINT: PRINT
5970 PRINT TAB(10);"GAME TIME ELAPSED";: PRINT TAB(50);"ESTIMATED TIME REMAINING"
5980 PRINT TAB(10);"31 HRS 12 MIN ";GTE%;" SEC";: PRINT TAB(50);"52 HRS 17 MIN ";ETR%;" SEC"
5990 FOR A%=1 TO 80: PRINT "_";: NEXT A%
6000 PRINT
6010 FOR W%=1 TO D%*25: NEXT W%
6020 NEXT GC%
6030 PRINT:PRINT
6040 INPUT "                          Press 'ENTER' key to CONTINUE",RESPONSE$
6050 PRINT CLS$: GOTO 4250
6060 PRINT CLS$
6070 PRINT "                           WOPR ( Wargames ) Simulator"
6080 PRINT "                           War Operation Plan Response"
6090 PRINT
6100 OPEN"I",1,F$
6110 LET COUNT%=1
6120 IF EOF(1) THEN GOTO 6170
6130 PRINT INPUT$(1,#1);
6140 LET COUNT%=COUNT%+1
6150 IF COUNT%=80 THEN LET COUNT%=0: PRINT
6160 GOTO 6120
6170 CLOSE
6180 PRINT: PRINT
6190 INPUT "                          Press 'ENTER' key to CONTINUE",RESPONSE$
6200 PRINT CLS$
6210 RETURN
6220 FOR W%=1 TO D%*25: NEXT W%: PRINT CLS$
6230 PRINT "DIALING";:GOSUB 5450
6240 PRINT "ERROR 676   LINE BUSY": FOR W%=1 TO D%*40: NEXT W%: PRINT CLS$
6250 RETURN
6260 PRINT CLS$
6270 PRINT "The Grid is Numbered"
6280 PRINT
6290 PRINT " 1 ! 2 ! 3 "
6300 PRINT "---+---+---"
6310 PRINT " 4 ! 5 ! 6 "
6320 PRINT "---+---+---"
6330 PRINT " 7 ! 8 ! 9 "
6340 PRINT
6350 INPUT "PRESS 'ENTER' KEY TO CONTINUE",RESPONSE$: PRINT CLS$
6360 FOR R=1 TO 9: LET A(R)=0: NEXT R
6370 PRINT"Who goes first (ME or YOU): ";
6380 LINE INPUT A$
6390 IF MID$(A$,1,1)="Y" OR MID$(A$,1,1)="y" THEN QE=1:P$=" OX":GOTO 6780
6400 PRINT"Do you want 'X' or 'O': ";:LINE INPUT M$
6410 IF M$="o" OR M$="O" THEN P$=" OX" ELSE P$=" XO"
6420 GOTO 6780
6430 REM Here is where somebody wins!!
6440 W=0:FOR E=1 TO 2:FOR X=1 TO 8:ON X GOTO 6450,6460,6470,6480,6490,6500,6510,6520
6450 J=1:K=2:L=3:GOTO 6530
6460 J=1:K=5:L=9:GOTO 6530
6470 J=3:K=5:L=7:GOTO 6530
6480 J=4:K=5:L=6:GOTO 6530
6490 J=7:K=8:L=9:GOTO 6530
6500 J=1:K=4:L=7:GOTO 6530
6510 J=2:K=5:L=8:GOTO 6530
6520 J=3:K=6:L=9
6530 IF A(J)=E AND A(K)=E AND A(L)=E THEN 6550
6540 NEXT X:NEXT E:W=0:GOTO 6560
6550 W=E:RETURN
6560 FOR E=1 TO 0:NEXT E:FOR X=1 TO 0:NEXT X
6570 FOR I=1 TO 9
6580 IF A(I)=0 THEN 6600
6590 NEXT I:W=-1:RETURN
6600 FOR I=1 TO 0:NEXT I:W=0:RETURN
6610 Z=0:IF A(M)<>0 THEN Z=1:RETURN ELSE RETURN
6620 REM Find a good move
6630 M=0:FOR E=2 TO 1 STEP -1:FOR X=1 TO 8:ON X GOTO 6640,6650,6660,6670,6680,6690,6700,6710
6640 J=1:K=2:L=3:GOTO 6720
6650 J=1:K=5:L=9:GOTO 6720
6660 J=3:K=5:L=7:GOTO 6720
6670 J=4:K=5:L=6:GOTO 6720
6680 J=7:K=8:L=9:GOTO 6720
6690 J=1:K=4:L=7:GOTO 6720
6700 J=2:K=5:L=8:GOTO 6720
6710 J=3:K=6:L=9
6720 IF A(J)=E AND A(K)=E THEN M=L:GOSUB 6610:IF Z=0 THEN 6770
6730 IF A(K)=E AND A(L)=E THEN M=J:GOSUB 6610:IF Z=0 THEN 6770
6740 IF A(J)=E AND A(L)=E THEN M=K:GOSUB 6610:IF Z=0 THEN 6770
6750 NEXT X:NEXT E:REM No good move found, pick one!
6760 M=INT(9*RND(1)+1):IF A(M)<>0 THEN 6760 ELSE RETURN
6770 FOR X=1 TO 0:NEXT X:FOR E=1 TO 2:NEXT E:RETURN
6780 A$="---+---+---"
6790 IF QE=1 THEN QE=0:GOTO 6890: ELSE 6840
6800 PRINT CLS$
6810 LET I=1: PRINT " "+MID$(P$,A(I)+1,1)+" ! "+MID$(P$,A(I+1)+1,1)+" ! "+MID$(P$,A(I+2)+1,1)+" ": PRINT A$
6820 LET I=4: PRINT " "+MID$(P$,A(I)+1,1)+" ! "+MID$(P$,A(I+1)+1,1)+" ! "+MID$(P$,A(I+2)+1,1)+" ": PRINT A$
6830 LET I=7: PRINT " "+MID$(P$,A(I)+1,1)+" ! "+MID$(P$,A(I+1)+1,1)+" ! "+MID$(P$,A(I+2)+1,1)+" ": RETURN
6840 GOSUB 6800: PRINT
6850 PRINT"Where do you move (1-9):";:INPUT M
6860 IF M<1 OR M>9 THEN PRINT:PRINT"Illegal square location!":PRINT:GOTO 6850
6870 IF A(M)<>0 THEN PRINT:PRINT"Square occupied!":PRINT:GOTO 6850
6880 A(M)=1
6890 GOSUB 6430: IF W<>0 THEN 6920
6900 PRINT"Hm......";:GOSUB 6620
6910 A(M)=2:PRINT"I move ";M:GOSUB 6430: IF W<>0 THEN 6920 ELSE GOTO 6840
6920 IF W=-1 THEN PRINT:PRINT"That was a tie game."
6930 IF W=1 THEN PRINT:PRINT"You win."
6940 IF W=2 THEN PRINT:PRINT"Ha-Ha!! I win!!":PRINT"Don't you know that you can't beat a computer!"
6950 PRINT:PRINT"Do you want to try again (Y/N):";
6960 LINE INPUT F$
6970 IF MID$(F$,1,1)="N" OR MID$(F$,1,1)="n" THEN 6980: ELSE: FOR R=1 TO 9: LET A(R)=0: NEXT R: GOTO 6260
6980 PRINT"BYE!!!"
6990 FOR W%=1 TO D%*20: NEXT W%
7000 PRINT CLS$: GOTO 4250
