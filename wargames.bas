10 REM *******************************************
20 REM *                                         *
30 REM *          WARGAMES SIMULATOR             *
40 REM *            BY ANDY GLENN                *
50 REM *     (C) 2020, ALL RIGHTS RESERVED       *
60 REM *                                         *
70 REM *******************************************
80 LET CLS$=CHR$(27)+"c"
90 DIM GRADE$(2)
100 DIM PFX%(9)
110 DIM NUM%(9)
120 DIM PFXHIT%(9)
130 DIM NUMHIT%(9)
140 DIM PASNAME$(2)
150 LET GRADE$(1)="F"
160 LET GRADE$(2)="F"
170 DIM COMPUTERTGT$(2)
180 DIM HUMANTGT$(3)
190 DIM A(9)
200 LET HT%=0
210 LET COMPUTERTGT$(1)=""
220 LET COMPUTERTGT$(2)=""
230 LET HUMANTGT$(1)=""
240 LET HUMANTGT$(2)=""
250 LET COMPUTER$=""
260 LET HUMAN$=""
270 LET CINDENT%=0
280 LET HINDENT%=0
290 LET SF%=0
300 PRINT CHR$(27)+"E"+CHR$(27)+"H": PRINT CHR$(27)+"c"
310 PRINT "WARGAMES SIMULATOR"
320 PRINT "(C) ANDY GLENN 2020, ALL RIGHTS RESERVED"
330 PRINT
340 INPUT "SELECT DELAY FOR TERMINAL CHARACTERS: ",D%
350 INPUT "DO YOU WANT HELP ENABLED (Y/N): ",H$
360 IF H$="N" OR H$="n" THEN LET H$="N": ELSE: LET H$="Y"
370 PRINT: PRINT "SELECT SYSTEM": PRINT
380 PRINT "1. IMSAI 8080 (DEFAULT)
390 PRINT "2. AMSTRAD PCW"
400 PRINT: INPUT "SYSTEM: ",S$
410 IF S$="2" THEN LET CLS$=CHR$(27)+"E"+CHR$(27)+"H"
420 IF H$="Y" THEN LET F$="INTRO.TXT": GOSUB 6140: ELSE: GOTO 440
430 IF H$="Y" THEN LET F$="IMSAI.TXT": GOSUB 6140: ELSE: GOTO 440
440 PRINT CLS$
450 PRINT"62K CP/M VERS. 2.2 MCL030210-D-F8"
460 PRINT
470 INPUT"A>",COMMAND$
480 IF COMMAND$="CLS" OR COMMAND$="cls" THEN PRINT CLS$: GOTO 470
490 IF COMMAND$="BYE" OR COMMAND$="bye" THEN PRINT "System halted, bye.": END
500 IF COMMAND$="DIR" OR COMMAND$="dir" THEN PRINT "A: BYE     COM : CLS     COM : DIR     COM: MODEM   COM": PRINT "A: DIALER  COM": GOTO 470
510 IF COMMAND$="MODEM" OR COMMAND$="modem" OR COMMAND$="MODEM.COM" OR COMMAND$="modem.com" THEN GOTO 540
520 IF COMMAND$="DIALER" OR COMMAND$="dialer" OR COMMAND$="DIALER.COM" OR COMMAND$="dialer.com" THEN GOTO 1260
530 PRINT COMMAND$;"?": GOTO 470
540 FOR W%=1 TO D%*10: NEXT W%: PRINT CLS$
550 PRINT "CHECKING DISK DRIVE";:GOSUB 5520
560 PRINT "LOADING DISK";:GOSUB 5520
570 PRINT "CHECKING DISK";:GOSUB 5520
580 PRINT "READING PROGRAM FROM DISK";:GOSUB 5520
590 FOR W%=1 TO D%*25: NEXT W%: PRINT CLS$
600 PRINT "DIALING";:GOSUB 5520
610 PRINT "CONNECTING";:GOSUB 5520
620 FOR W%=1 TO D%*20: NEXT W%: PRINT CLS$
630 LET T$="PDP 11/270 PRB TIP #45                                                TTY 34/984 ": GOSUB 5530: PRINT
640 LET T$="WELCOME TO THE SEATTLE PUBLIC SCHOOL DISTRICT DATANET": GOSUB 5530: PRINT
650 FOR P%=1 TO 3
660 LET T$="PLEASE LOGON WITH USER PASSWORD: ": GOSUB 5530: INPUT "",RESPONSE$
670 IF RESPONSE$="PENCIL" OR RESPONSE$="pencil" THEN LET T$="PASSWORD VERIFIED": GOSUB 5530: PRINT: PRINT: GOTO 740
680 LET T$="INVALID PASSWORD": GOSUB 5530: PRINT: PRINT
690 NEXT P%
700 LET T$="TOO MANY INVALID PASSWORD ATTEMPTS": GOSUB 5530: PRINT
710 LET T$="--DISCONNECTED--": GOSUB 5530: INPUT "",RESPONSE$
720 IF H$="Y" AND GRADE$(1)<>"F" AND GRADE$(2)<>"F" THEN LET F$="GRADES.TXT": GOSUB 6140
730 GOTO 440
740 LET T$="PLEASE ENTER STUDENT NAME: ": GOSUB 5530: INPUT "",STUDENTNAME$
750 FOR A%=1 TO LEN(STUDENTNAME$): IF MID$(STUDENTNAME$,A%,8)="Lightman" OR MID$(STUDENTNAME$,A%,8)="LIGHTMAN" OR MID$(STUDENTNAME$,A%,8)="lightman" THEN LET STUDENTNAME$="Lightman, David L.":NEXT A%
760 IF STUDENTNAME$="Lightman, David L." THEN GOTO 810
770 FOR A%=1 TO LEN(STUDENTNAME$): IF MID$(STUDENTNAME$,A%,4)="Mack" OR MID$(STUDENTNAME$,A%,4)="MACK" OR MID$(STUDENTNAME$,A%,4)="mack" THEN LET STUDENTNAME$="Mack, Jennifer K.": NEXT A%
780 IF STUDENTNAME$="Mack, Jennifer K." THEN GOTO 960
790 IF STUDENTNAME$="Exit" OR STUDENTNAME$="EXIT" OR STUDENTNAME$="exit" THEN GOTO 710
800 LET T$="NO STUDENT RECORD FOUND": GOSUB 5530: PRINT: GOTO 740
810 LET STUDENT%=1
820 PRINT
830 LET T$="STUDENT RECORD: "+STUDENTNAME$: GOSUB 5530: PRINT
840 PRINT
850 LET T$="     CLASS #    COURSE TITLE         GRADE    TEACHER    PERIOD    ROOM": GOSUB 5530: PRINT
860 LET T$="   ______________________________________________________________________": GOSUB 5530: PRINT
870 PRINT
880 LET T$="      S-202     BIOLOGY 2              "+GRADE$(STUDENT%)+"      LIGGET       3       214": GOSUB 5530: PRINT
890 LET T$="      E-314     ENGLISH 11B            D      TURMAN       5       172": GOSUB 5530: PRINT
900 LET T$="      H-221     WORLD HISTORY 11B      C      DWYMER       2       108": GOSUB 5530: PRINT
910 LET T$="      M-106     TRIG 2                 B      DICKERSON    4       315": GOSUB 5530: PRINT
920 LET T$="      PE-02     PHYSICAL EDUCATION     C      COMSTOCK     1       GYM": GOSUB 5530: PRINT
930 LET T$="      M-122     CALCULUS 1             B      LOGAN        6       240": GOSUB 5530: PRINT
940 PRINT
950 GOTO 1100
960 LET STUDENT%=2
970 PRINT
980 LET T$="STUDENT RECORD: "+STUDENTNAME$: GOSUB 5530: PRINT
990 PRINT
1000 LET T$="     CLASS #    COURSE TITLE         GRADE    TEACHER    PERIOD    ROOM": GOSUB 5530: PRINT
1010 LET T$="   ______________________________________________________________________": GOSUB 5530: PRINT
1020 PRINT
1030 LET T$="      S-202     BIOLOGY 2              "+GRADE$(STUDENT%)+"      LIGGET       3       214": GOSUB 5530: PRINT
1040 LET T$="      E-325     ENGLISH 11B            A      ROBINSON     1       114": GOSUB 5530: PRINT
1050 LET T$="      H-221     WORLD HISTORY 11B      B      DWYER        2       108": GOSUB 5530: PRINT
1060 LET T$="      M-104     GEOMETRY 2             D      HALQUIST     4       307": GOSUB 5530: PRINT
1070 LET T$="      B-107     ECONOMICS              D      MARKS        5       122": GOSUB 5530: PRINT
1080 LET T$="      PE-02     PHYSICAL EDUCATION     C      COMSTOCK     6       GYM": GOSUB 5530: PRINT
1090 PRINT
1100 LET T$="(E)DIT: ": GOSUB 5530: INPUT "",RESPONSE$
1110 IF RESPONSE$="E" OR RESPONSE$="e" OR RESPONSE$="edit" THEN GOTO 1130: ELSE : PRINT: GOTO 740
1120 PRINT
1130 LET TEMPGRADE$=GRADE$(STUDENT%)
1140 LET T$="CLASS: ": GOSUB 5530: INPUT "",CLASS$
1150 LET T$="GRADE: ": GOSUB 5530: INPUT "",GRADE$(STUDENT%)
1160 IF CLASS$="S-202" OR CLASS$="s-202" THEN GOTO 1180
1170 PRINT: LET T$="RECORD LOCKED": GOSUB 5530: LET GRADE$(STUDENT%)=TEMPGRADE$: GOTO 1090
1180 IF GRADE$(STUDENT%)="a" OR GRADE$(STUDENT%)="A" THEN LET GRADE$(STUDENT%)="A": PRINT: LET T$="RECORD UPDATED": GOSUB 5530: PRINT: GOTO 740
1190 IF GRADE$(STUDENT%)="b" OR GRADE$(STUDENT%)="B" THEN LET GRADE$(STUDENT%)="B": PRINT: LET T$="RECORD UPDATED": GOSUB 5530: PRINT: GOTO 740
1200 IF GRADE$(STUDENT%)="c" OR GRADE$(STUDENT%)="C" THEN LET GRADE$(STUDENT%)="C": PRINT: LET T$="RECORD UPDATED": GOSUB 5530: PRINT: GOTO 740
1210 IF GRADE$(STUDENT%)="d" OR GRADE$(STUDENT%)="D" THEN LET GRADE$(STUDENT%)="D": PRINT: LET T$="RECORD UPDATED": GOSUB 5530: PRINT: GOTO 740
1220 IF GRADE$(STUDENT%)="e" OR GRADE$(STUDENT%)="E" THEN LET GRADE$(STUDENT%)="E": PRINT: LET T$="RECORD UPDATED": GOSUB 5530: PRINT: GOTO 740
1230 IF GRADE$(STUDENT%)="f" OR GRADE$(STUDENT%)="F" THEN LET GRADE$(STUDENT%)="F": PRINT: LET T$="RECORD UPDATED": GOSUB 5530: PRINT: GOTO 740
1240 IF GRADE$(STUDENT%)="u" OR GRADE$(STUDENT%)="U" THEN LET GRADE$(STUDENT%)="U": PRINT: LET T$="RECORD UPDATED": GOSUB 5530: PRINT: GOTO 740
1250 PRINT: LET T$="RECORD LOCKED": GOSUB 5530: LET GRADE$(STUDENT%)=TEMPGRADE$: GOTO 1090
1260 LET HITS%=0
1270 LET PF1%=399
1280 LET PF2%=437
1290 LET PF3%=767
1300 LET PF4%=936
1310 DATA 437,1083
1320 DATA 936,1493
1330 DATA 399,2364
1340 DATA 437,2977
1350 DATA 767,3395
1360 DATA 399,3582
1370 DATA 936,3923
1380 DATA 767,7305
1390 DATA 437,8739
1400 RESTORE
1410 FOR A%=1 TO 9
1420 READ PFX%(A%)
1430 READ NUM%(A%)
1440 NEXT A%
1450 PRINT CLS$
1460 PRINT: PRINT "DIALER": PRINT
1470 INPUT "MENU - (S)CAN, (V)IEW OR E(X)IT: ",RESPONSE$
1480 PRINT
1490 IF RESPONSE$="S" OR RESPONSE$="s" OR RESPONSE$="SCAN" OR RESPONSE$="scan" THEN GOTO 1530
1500 IF RESPONSE$="V" OR RESPONSE$="v" OR RESPONSE$="VIEW" OR RESPONSE$="view" THEN GOTO 2430
1510 IF RESPONSE$="X" OR RESPONSE$="x" OR RESPONSE$="EXIT" OR RESPONSE$="exit" THEN GOTO 440
1520 GOTO 1450
1530 INPUT "HOW MANY NUMBERS FOR EACH PREFIX TO DIAL: ",ND%
1540 IF ND% > 9999 THEN PRINT "MAXIMUM IS 9999": GOTO 1530
1550 LET LN%=0
1560 LET HITS%=0
1570 LET PFXSET%=0
1580 FOR A% = 1 TO ND%
1590 IF LN%=0 THEN PRINT CLS$
1600 IF LN%=0 THEN PRINT "                    TO SCAN FOR CARRIER TONES, PLEASE LIST"
1610 IF LN%=0 THEN PRINT"                         DESIRED AREA CODES AND PREFIXES"
1620 IF LN%=0 THEN PRINT
1630 IF LN%=0 THEN PRINT "AREA                AREA                AREA                AREA"
1640 IF LN%=0 THEN PRINT "CODE PRFX NUMBER    CODE PRFX NUMBER    CODE PRFX NUMBER    CODE PRFX NUMBER"
1650 IF LN%=0 THEN PRINT "________________________________________________________________________________": PRINT
1660 IF LN%=0 AND A%=1 THEN PRINT "(311)";PF1%;"          (311)";PF2%;"          (311)";PF3%;"          (311)";PF4%
1670 IF PFXSET%=1 THEN GOTO 1800
1680 IF LN%=0 AND A%=1 THEN INPUT "(311) ",PF1T%
1690 IF LN%=0 AND A%=1 THEN INPUT "                    (311) ",PF2T%
1700 IF LN%=0 AND A%=1 THEN INPUT "                                        (311) ",PF3T%
1710 IF LN%=0 AND A%=1 THEN INPUT "                                                            (311) ",PF4T%
1720 IF LN%=0 AND A%=1 THEN PRINT
1730 IF LN%=0 AND A%=1 AND PF1T%>0 AND PF1T%<1000 THEN LET PF1%=PF1T%
1740 IF LN%=0 AND A%=1 AND PF2T%>0 AND PF2T%<1000 THEN LET PF2%=PF2T%
1750 IF LN%=0 AND A%=1 AND PF3T%>0 AND PF3T%<1000 THEN LET PF3%=PF3T%
1760 IF LN%=0 AND A%=1 AND PF4T%>0 AND PF4T%<1000 THEN LET PF4%=PF4T%
1770 IF PF1T%>999 OR PF2T%>999 OR PF3T%>999 OR PF4T%>999 THEN PRINT: INPUT "INVALID PREFIXES ENTERED - PRESS ENTER KEY TO CONTINUE",RESPONSE$: PRINT: GOTO 1550
1780 IF LN%=0 AND A%=1 THEN LET PFXSET%=1: GOTO 1590
1790 IF INKEY$="Q" OR INKEY$="q" THEN GOTO 2370
1800 IF A% > 0 AND A% < 10 THEN Z$="000"
1810 IF A% > 9 AND A% < 100 THEN Z$="00"
1820 IF A% > 99 AND A% < 1000 THEN Z$="0"
1830 IF A% > 999 THEN Z$=""
1840 PRINT "(311)";PF1%;Z$;A%;
1850 LET FD%=0
1860 LET HIT$="N"
1870 FOR N%=1 TO 9
1880 IF INKEY$="Q" OR INKEY$="q" THEN GOTO 2370
1890 IF PF1%=PFX%(N%) AND A%=NUM%(N%) THEN LET HIT$="Y"
1900 IF PF1%=PFX%(N%) AND A%=NUM%(N%) THEN LET FD%=N%
1910 NEXT N%
1920 IF HIT$="Y" THEN LET HITS%=HITS%+1
1930 IF HIT$="Y" THEN LET PFXHIT%(HITS%)=PFX%(FD%)
1940 IF HIT$="Y" THEN LET NUMHIT%(HITS%)=NUM%(FD%)
1950 IF HIT$="Y" THEN PRINT "*   ";: ELSE: PRINT "    ";
1960 LET FD%=0
1970 LET HIT$="N"
1980 PRINT "(311)";PF2%;Z$;A%;
1990 FOR N%=1 TO 9
2000 IF INKEY$="Q" OR INKEY$="q" THEN GOTO 2370
2010 IF PF2%=PFX%(N%) AND A%=NUM%(N%) THEN LET HIT$="Y"
2020 IF PF2%=PFX%(N%) AND A%=NUM%(N%) THEN LET FD%=N%
2030 NEXT N%
2040 IF HIT$="Y" THEN LET HITS%=HITS%+1
2050 IF HIT$="Y" THEN LET PFXHIT%(HITS%)=PFX%(FD%)
2060 IF HIT$="Y" THEN LET NUMHIT%(HITS%)=NUM%(FD%)
2070 IF HIT$="Y" THEN PRINT "*   ";: ELSE: PRINT "    ";
2080 LET FD%=0
2090 LET HIT$="N"
2100 PRINT "(311)";PF3%;Z$;A%;
2110 FOR N%=1 TO 9
2120 IF INKEY$="Q" OR INKEY$="q" THEN GOTO 2370
2130 IF PF3%=PFX%(N%) AND A%=NUM%(N%) THEN LET HIT$="Y"
2140 IF PF3%=PFX%(N%) AND A%=NUM%(N%) THEN LET FD%=N%
2150 NEXT N%
2160 IF HIT$="Y" THEN LET HITS%=HITS%+1
2170 IF HIT$="Y" THEN LET PFXHIT%(HITS%)=PFX%(FD%)
2180 IF HIT$="Y" THEN LET NUMHIT%(HITS%)=NUM%(FD%)
2190 IF HIT$="Y" THEN PRINT "*   ";: ELSE: PRINT "    ";
2200 LET FD%=0
2210 LET HIT$="N"
2220 PRINT "(311)";PF4%;Z$;A%;
2230 FOR N%=1 TO 9
2240 IF INKEY$="Q" OR INKEY$="q" THEN GOTO 2370
2250 IF PF4%=PFX%(N%) AND A%=NUM%(N%) THEN LET HIT$="Y"
2260 IF PF4%=PFX%(N%) AND A%=NUM%(N%) THEN LET FD%=N%
2270 NEXT N%
2280 IF HIT$="Y" THEN LET HITS%=HITS%+1
2290 IF HIT$="Y" THEN LET PFXHIT%(HITS%)=PFX%(FD%)
2300 IF HIT$="Y" THEN LET NUMHIT%(HITS%)=NUM%(FD%)
2310 IF HIT$="Y" THEN PRINT "*": ELSE: PRINT " "
2320 LET FD%=0
2330 LET HIT$="N"
2340 LET LN%=LN%+1
2350 IF LN%=15 THEN LET LN%=0
2360 NEXT A%
2370 IF HITS%>SF% THEN LET SF%=HITS%
2380 PRINT: INPUT "                      PRESS ENTER KEY TO RETURN TO MENU",RESPONSE$
2390 IF SF%=0 THEN GOTO 1450
2400 PRINT CLS$
2410 IF H$="Y" AND HITS%>0 THEN LET F$="DIALER.TXT": GOSUB 6140
2420 GOTO 1450
2430 PRINT CLS$
2440 PRINT "                  NUMBERS FOR WHICH CARRIER TONES WERE DETECTED:"
2450 PRINT
2460 IF SF%=0 THEN PRINT "                                NO SYSTEMS FOUND"
2470 IF SF%=0 THEN PRINT: PRINT "         NOTE: TYPE LOAD AND PRESS ENTER KEY TO RESTORE SCAN RESULTS"
2480 FOR A%=1 TO SF%
2490 IF NUMHIT%(A%) >0 AND NUMHIT%(A%) <10 THEN Z$="000"
2500 IF NUMHIT%(A%) >9 AND NUMHIT%(A%) <100 THEN Z$="00"
2510 IF NUMHIT%(A%) >99 AND NUMHIT%(A%) <1000 THEN Z$="0"
2520 IF NUMHIT%(A%) >999 THEN Z$=""
2530 PRINT "                              (";CHR$(A%+64);")  (311)";PFXHIT%(A%);Z$;NUMHIT%(A%)
2540 NEXT A%
2550 PRINT: PRINT: PRINT "                        PRESS ENTER KEY TO RETURN TO MENU"
2560 INPUT "                        OR SELECT SYSTEM: ",RESPONSE$
2570 IF RESPONSE$="LOAD" OR RESPONSE$="load" THEN LET SF%=9: RESTORE: FOR A%=1 TO SF%: READ PFXHIT%(A%):READ NUMHIT%(A%): NEXT A%: GOTO 2430
2580 IF SF%=0 THEN GOTO 2760
2590 IF RESPONSE$="A" OR RESPONSE$="a" THEN GOSUB 6300: GOTO 2430
2600 IF SF%=1 THEN GOTO 2760
2610 IF RESPONSE$="B" OR RESPONSE$="b" THEN GOSUB 6300: GOTO 2430
2620 IF SF%=2 THEN GOTO 2760
2630 IF RESPONSE$="C" OR RESPONSE$="c" THEN GOTO 2770
2640 IF SF%=3 THEN GOTO 2760
2650 IF RESPONSE$="D" OR RESPONSE$="d" THEN GOSUB 6300: GOTO 2430
2660 IF SF%=4 THEN GOTO 2760
2670 IF RESPONSE$="E" OR RESPONSE$="e" THEN GOSUB 6300: GOTO 2430
2680 IF SF%=5 THEN GOTO 2760
2690 IF RESPONSE$="F" OR RESPONSE$="f" THEN GOTO 2900
2700 IF SF%=6 THEN GOTO 2760
2710 IF RESPONSE$="G" OR RESPONSE$="g" THEN GOSUB 6300: GOTO 2430
2720 IF SF%=7 THEN GOTO 2760
2730 IF RESPONSE$="H" OR RESPONSE$="h" THEN GOSUB 6300: GOTO 2430
2740 IF SF%=8 THEN GOTO 2760
2750 IF RESPONSE$="I" OR RESPONSE$="i" THEN GOTO 3770
2760 GOTO 1450
2770 PRINT CLS$
2780 PRINT "DIALING";:GOSUB 5520: PRINT CLS$
2790 PRINT "CONNECTING";:GOSUB 5520: PRINT CLS$
2800 FOR W%=1 TO D%*20: NEXT W%: PRINT CLS$
2810 LET T$="TGS SYSTEM A-45 34:34:33                                              Y-1293.323 ": GOSUB 5530: PRINT
2820 LET T$="UNION MARINE BANK - SOUTHWEST REGIONAL DATA CENTER": GOSUB 5530: PRINT
2830 PRINT
2840 LET T$="LOGON    > ": GOSUB 5530: INPUT "",RESPONSE$
2850 LET T$="PASSWORD > ": GOSUB 5530: INPUT "",RESPONSE$
2860 PRINT
2870 LET T$="INVALID LOGON. CLOSING CONNECTION": GOSUB 5530: PRINT
2880 LET T$="--DISCONNECTED--": GOSUB 5530: INPUT "",RESPONSE$
2890 GOTO 2430
2900 PRINT CLS$
2910 PRINT "DIALING";:GOSUB 5520: PRINT CLS$
2920 PRINT "CONNECTING";:GOSUB 5520: PRINT CLS$
2930 FOR W%=1 TO D%*20: NEXT W%: PRINT CLS$
2940 LET T$="007:2312:435:936                                       PAN-AM RESERVATION SYSTEM": GOSUB 5530: PRINT
2950 LET T$="TERMINAL STATUS: ACTIVE": GOSUB 5530: PRINT
2960 PRINT
2970 LET T$="  1.  FLIGHT SCHEDULES": GOSUB 5530: PRINT
2980 LET T$="  2.  CREDIT CHECK": GOSUB 5530: PRINT
2990 LET T$="  3.  PASSENGER INFORMATION": GOSUB 5530: PRINT
3000 LET T$="  4.  SYSTEM STATUS FUNCTIONS": GOSUB 5530: PRINT
3010 LET T$="  5.  ALTERNATE ROUTINGS": GOSUB 5530: PRINT
3020 PRINT
3030 LET T$="MENU CHOICE: ": GOSUB 5530: INPUT "",RESPONSE$
3040 IF RESPONSE$="1" THEN GOTO 3730
3050 IF RESPONSE$="2" THEN GOTO 3740
3060 IF RESPONSE$="3" THEN GOTO 3120
3070 IF RESPONSE$="4" THEN GOTO 3750
3080 IF RESPONSE$="5" THEN GOTO 3760
3090 LET T$="CLOSING CONNECTION": GOSUB 5530: PRINT
3100 LET T$="--DISCONNECTED--": GOSUB 5530: INPUT "",RESPONSE$
3110 GOTO 2430
3120 PRINT CLS$
3130 LET T$="007:2312:435:936                                       PAN-AM RESERVATION SYSTEM": GOSUB 5530: PRINT
3140 PRINT
3150 LET T$="  1.  NEW RESERVATION": GOSUB 5530: PRINT
3160 LET T$="  2.  CHECK RESERVATION": GOSUB 5530: PRINT
3170 PRINT
3180 LET T$="MENU CHOICE: ": GOSUB 5530: INPUT "",RESPONSE$
3190 IF RESPONSE$="1" THEN GOTO 3220
3200 IF RESPONSE$="2" THEN GOTO 3470
3210 GOTO 2930
3220 PRINT CLS$
3230 LET T$="007:2312:435:936                                       PAN-AM RESERVATION SYSTEM": GOSUB 5530: PRINT
3240 LET T$="DESTINATION         : ": GOSUB 5530: INPUT "",DEST$
3250 LET T$="POINT OF DEPARTURE  : CHICAGO/O'HARE": GOSUB 5530: PRINT
3260 PRINT
3270 LET T$="009:4277:019:907                                        DATE OF ISSUE : 03/10/82": GOSUB 5530: PRINT
3280 PRINT
3290 LET T$="                                           DEPART DATE : 08/18/83    FLIGHT: 114": GOSUB 5530: PRINT
3300 PRINT
3310 LET T$="                            CARRIER:   FLIGHT:   CLASS:   DATE:   TIME:  STATUS:": GOSUB 5530: PRINT
3320 LET T$="                              PANAM       114       Q     18AUG  0815A    OK": GOSUB 5530: PRINT
3330 LET T$="FROM: CHICAGO/O'HARE": GOSUB 5530: PRINT 
3340 LET T$="  TO: "+DEST$: GOSUB 5530: PRINT
3350 PRINT
3360 LET T$="NUMBER OF PASSENGERS  : ": GOSUB 5530: INPUT "",NUMPAS%
3370 FOR PAS%=1 TO NUMPAS%
3380 LET T$="NAME OF PASSENGER #": GOSUB 5530: PRINT PAS%;: INPUT ": ",PASNAME$(PAS%)
3390 NEXT PAS%
3400 PRINT
3410 LET T$="FARE:  ": GOSUB 5530: PRINT USING "####.##";1164.1*NUMPAS%;
3420 LET T$="   GJATWA  SLM  TWA  GJA": GOSUB 5530: PRINT
3430 LET T$="TAX:   ": GOSUB 5530: PRINT USING "####.##";70.9*NUMPAS%
3440 LET T$="TOTAL: ": GOSUB 5530: PRINT USING "####.##";(1164.1+70.9)*NUMPAS%
3450 LET T$="REQUEST: SPEC DATA": GOSUB 5530: PRINT
3460 LET T$="                         PRESS 'ENTER' KEY TO RETURN TO MENU": GOSUB 5530: INPUT "",RESPONSE$: GOTO 3120
3470 PRINT CLS$
3480 LET T$="007:2312:435:936                                       PAN-AM RESERVATION SYSTEM": GOSUB 5530: PRINT
3490 LET T$="DESTINATION         : "+DEST$: GOSUB 5530: PRINT
3500 LET T$="POINT OF DEPARTURE  : CHICAGO/O'HARE": GOSUB 5530: PRINT
3510 PRINT
3520 LET T$="009:4277:019:907                                        DATE OF ISSUE : 03/10/82": GOSUB 5530: PRINT
3530 PRINT
3540 LET T$="                                           DEPART DATE : 08/18/83    FLIGHT: 114": GOSUB 5530: PRINT
3550 PRINT
3560 LET T$="                            CARRIER:   FLIGHT:   CLASS:   DATE:   TIME:  STATUS:": GOSUB 5530: PRINT
3570 LET T$="                              PANAM       114       Q     18AUG  0815A    OK": GOSUB 5530: PRINT
3580 LET T$="FROM: CHICAGO/O'HARE": GOSUB 5530: PRINT 
3590 LET T$="  TO: "+DEST$: GOSUB 5530: PRINT
3600 PRINT
3610 LET T$="NUMBER OF PASSENGERS   :": GOSUB 5530: PRINT NUMPAS%
3620 FOR PAS%=1 TO NUMPAS%
3630 LET T$="NAME OF PASSENGER #": GOSUB 5530: PRINT PAS%;" : ";
3640 LET T$=PASNAME$(PAS%): GOSUB 5530: PRINT
3650 NEXT PAS%
3660 PRINT
3670 LET T$="FARE:  ": GOSUB 5530: PRINT USING "####.##";1164.1*NUMPAS%;
3680 LET T$="   GJATWA  SLM  TWA  GJA": GOSUB 5530: PRINT
3690 LET T$="TAX:   ": GOSUB 5530: PRINT USING "####.##";70.9*NUMPAS%
3700 LET T$="TOTAL: ": GOSUB 5530: PRINT USING "####.##";(1164.1+70.9)*NUMPAS%
3710 LET T$="REQUEST: SPEC DATA": GOSUB 5530: PRINT
3720 LET T$="                         PRESS 'ENTER' KEY TO RETURN TO MENU": GOSUB 5530: INPUT "",RESPONSE$: GOTO 3120
3730 PRINT CLS$: LET T$="NOT YET IMPLEMENTED": GOSUB 5530: GOTO 2930
3740 PRINT CLS$: LET T$="NOT YET IMPLEMENTED": GOSUB 5530: GOTO 2930
3750 PRINT CLS$: LET T$="NOT YET IMPLEMENTED": GOSUB 5530: GOTO 2930
3760 PRINT CLS$: LET T$="NOT YET IMPLEMENTED": GOSUB 5530: GOTO 2930
3770 PRINT CLS$
3780 PRINT "DIALING";:GOSUB 5520
3790 PRINT "CONNECTING";:GOSUB 5520
3800 FOR W%=1 TO D%*20: NEXT W%: PRINT CLS$
3810 PRINT "                         #":FOR W%=1 TO D%*5: NEXT W%: PRINT CLS$
3820 PRINT: PRINT "                                           #":FOR W%=1 TO D%*5: NEXT W%: PRINT CLS$
3830 PRINT: PRINT: PRINT "                                                          #":FOR W%=1 TO D%*5: NEXT W%: PRINT CLS$
3840 PRINT: PRINT: PRINT
3850 LET LOGON$=""
3860 LET T$="LOGON: ": GOSUB 5530: INPUT "",LOGON$
3870 IF LOGON$="joshua" OR LOGON$="JOSHUA" THEN GOTO 3960
3880 IF LOGON$="help" OR LOGON$="HELP" THEN GOSUB 4740: GOTO 3850
3890 IF LOGON$="help logon" OR LOGON$="HELP LOGON" THEN GOSUB 4740: GOTO 3850
3900 IF LOGON$="help games" OR LOGON$="HELP GAMES" THEN GOSUB 4770: GOTO 3850
3910 IF LOGON$="list games" OR LOGON$="LIST GAMES" THEN GOSUB 4810: GOTO 3850
3920 PRINT "IDENTIFICAION NOT RECOGNIZED BY SYSTEM"
3930 PRINT "--CONNECTION TERMINATED--"
3940 INPUT "",RESPONSE$
3950 GOTO 2430
3960 PRINT CLS$
3970 PRINT "145          11456          11889          11893": FOR W%=1 TO D%: NEXT W%
3980 PRINT "PRT CON. 3.4.5. SECTRAN 9.4.3.          PORT STAT: SB-345": FOR W%=1 TO D%: NEXT W%
3990 PRINT: FOR W%=1 TO D%: NEXT W%: PRINT CLS$
4000 PRINT "(311) 655-7385": FOR W%=1 TO D%: NEXT W%
4010 PRINT: FOR W%=1 TO D%: NEXT W%: PRINT: FOR W%=1 TO D%: NEXT W%: PRINT CLS$: PRINT: FOR W%=1 TO D%: NEXT W%: PRINT: FOR W%=1 TO D%: NEXT W%: PRINT CLS$: PRINT: PRINT: FOR W%=1 TO D%: NEXT W%: PRINT CLS$
4020 PRINT "(311) 767-8739": FOR W%=1 TO D%: NEXT W%
4030 PRINT "(311) 936-2364": FOR W%=1 TO D%: NEXT W%: PRINT CLS$
4040 PRINT "PRT. STAT.                   CRT. DEF.": FOR W%=1 TO D%: NEXT W%
4050 PRINT "================================================"
4060 PRINT "FSKJJSJ: SUSJKJ: SUFJSL:          DKSJL: SKFJJ: SDKFJLJ": FOR W%=1 TO D%: NEXT W%
4070 PRINT "SYSPROC FUNCT READY          ALT NET READY": FOR W%=1 TO D%: NEXT W%
4080 PRINT "CPU AUTH RY-345-AX3     SYSCOMP STATUS: ALL PORTS ACTIVE": FOR W%=1 TO D%: NEXT W%
4090 PRINT "22/34534.90/3289               CVB-3904-39490": FOR W%=1 TO D%: NEXT W%
4100 PRINT "(311) 936-2384":FOR W%=1 TO D%: NEXT W%
4110 PRINT "(311) 936-3582": FOR W%=1 TO D%: NEXT W%: PRINT CLS$
4120 PRINT "22/34534.3209                  CVB-3904-39490": FOR W%=1 TO D%: NEXT W%
4130 PRINT "12934-AD-43KJ: CENTR PAK":  FOR W%=1 TO D%: NEXT W%
4140 PRINT "(311) 767-1083": FOR W%=1 TO D%: NEXT W%: PRINT CLS$
4150 PRINT "FLD CRS: 33.34.543     HPBS: 34/56/67/83     STATUS FLT  034/304": FOR W%=1 TO D%: NEXT W%: PRINT CLS$
4160 PRINT "1105-45-F6-B456          NOPR STATUS: TRAK OFF     PRON ACTIVE": FOR W%=1 TO D%: NEXT W%
4170 PRINT "(45:45:45  WER: 45/29/01 XCOMP: 43239582 YCOMP: 3492930D ZCOMP: 343906834": FOR W%=1 TO D%: NEXT W%
4180 PRINT "                                          SRON: 65=65/74/84/65/89": FOR W%=1 TO D%: NEXT W%: PRINT CLS$
4190 PRINT "-           PRT. STAT.                        CRY. DEF.": FOR W%=1 TO D%: NEXT W%
4200 PRINT "(311) 936-1582==============================================": FOR W%=1 TO D%: NEXT W%
4210 PRINT "                  3453                3594": FOR W%=1 TO D%: NEXT W%: PRINT CLS$
4220 PRINT "FLJ42   TK01   BM90   R601   6J82   FP03   ZWO::   JW89": FOR W%=1 TO D%: NEXT W%
4230 PRINT "DEF TRPCON: 43.45342.349": FOR W%=1 TO D%: NEXT W%
4240 PRINT "SYSPROC FUNCT READY                            ALT NET READY":FOR W%=1 TO D%: NEXT W%
4250 PRINT: FOR W=1 TO 250: NEXT W
4260 PRINT "CPU AUTH RY-345-AX3     SYSCOMP STATUS: ALL PORTS ACTIVE": FOR W%=1 TO D%: NEXT W%
4270 PRINT "(311) 936-2364": FOR W%=1 TO D%: NEXT W%
4280 PRINT "**********************************************************************": FOR W%=1 TO D%: NEXT W%
4290 PRINT "1105-45-F6-B456                 NOPR STATUS: TRAK OFF   PRON ACTIVE": PRINT CLS$: FOR W%=1 TO D%: NEXT W%
4300 PRINT CLS$
4310 LET T$="GREETINGS PROFESSOR FALKEN.": GOSUB 5530: PRINT: PRINT
4320 INPUT "",JOSHUA$
4330 IF JOSHUA$="chat" OR JOSHUA$="CHAT" THEN GOSUB 4450: GOTO 4320
4340 FOR A%=1 TO LEN(JOSHUA$)
4350 IF MID$(JOSHUA$,A%,5)="hello" OR MID$(JOSHUA$,A%,5)="HELLO" THEN GOSUB 4450
4360 NEXT A%
4370 IF JOSHUA$="help games" OR JOSHUA$="HELP GAMES" THEN GOSUB 4770: GOTO 4320
4380 IF JOSHUA$="list games" OR JOSHUA$="LIST GAMES" THEN GOSUB 4810: GOTO 4320
4390 IF JOSHUA$="help" OR JOSHUA$="HELP" THEN GOSUB 4740: GOTO 4320
4400 IF JOSHUA$="GLOBAL THERMONUCLEAR WAR" OR JOSHUA$="global thermonuclear war" THEN GOTO 4970
4410 IF JOSHUA$="TICTACTOE" OR JOSHUA$="tictactoe" THEN GOTO 6340
4420 IF JOSHUA$="exit" OR JOSHUA$="EXIT" THEN PRINT: LET T$="GOODBYE PROFESSOR FALKEN": GOSUB 5530: PRINT: PRINT: GOTO 3930
4430 IF JOSHUA$="" THEN GOTO 4320
4440 PRINT: PRINT JOSHUA$;"?": PRINT: GOTO 4320
4450 PRINT: LET T$="HOW ARE YOU FEELING TODAY?": GOSUB 5530: PRINT: PRINT
4460 INPUT "",JOSHUA$
4470 FOR A%=1 TO LEN(JOSHUA$)
4480 IF MID$(JOSHUA$,A%,4)="fine" OR MID$(JOSHUA$,A%,4)="FINE" THEN GOTO 4510
4490 NEXT A%
4500 GOTO 4730
4510 PRINT: LET T$="EXCELLENT. IT'S BEEN A LONG TIME. CAN YOU EXPLAIN THE REMOVAL OF YOUR USER": GOSUB 5530
4520 PRINT: LET T$="ACCOUNT ON 6/23/1973?": GOSUB 5530: PRINT: PRINT
4530 INPUT "",JOSHUA$
4540 FOR A%=1 TO LEN(JOSHUA$)
4550 IF MID$(JOSHUA$,A%,8)="mistakes" OR MID$(JOSHUA$,A%,8)="MISTAKES" THEN GOTO 4580
4560 NEXT A%
4570 GOTO 4730
4580 PRINT: LET T$="YES THEY DO. ": GOSUB 5530
4590 FOR W%=1 TO D%*20: NEXT W%
4600 LET T$="SHALL WE PLAY A GAME?": GOSUB 5530: PRINT: PRINT
4610 INPUT "",JOSHUA$
4620 FOR A%=1 TO LEN(JOSHUA$)
4630 IF MID$(JOSHUA$,A%,7)="nuclear" OR MID$(JOSHUA$,A%,7)="NUCLEAR" THEN GOTO 4660
4640 NEXT A%
4650 GOTO 4730
4660 PRINT: LET T$="WOULDN'T YOU PREFER A GOOD GAME OF CHESS?": GOSUB 5530: PRINT: PRINT
4670 INPUT "",JOSHUA$
4680 FOR A%=1 TO LEN(JOSHUA$)
4690 IF MID$(JOSHUA$,A%,5)="later" OR MID$(JOSHUA$,A%,5)="LATER" THEN GOTO 4720
4700 NEXT A%: 
4710 GOTO 4730
4720 LET JOSHUA$="": PRINT: LET T$="FINE": GOSUB 5530: PRINT: PRINT: FOR W%=1 TO D%*20: NEXT W%: GOTO 4970
4730 RETURN 
4740 PRINT: FOR W%=1 TO 250: NEXT W%
4750 LET T$="NO HELP AVAILABLE": GOSUB 5530: PRINT: PRINT
4760 RETURN
4770 PRINT: FOR W%=1 TO 250: NEXT W%
4780 LET T$="'GAMES' REFERS TO MODELS, SIMULATIONS, AND GAMES WHICH HAVE TACTICAL AND": GOSUB 5530: PRINT
4790 LET T$="STRATEGIC APPLICATIONS": GOSUB 5530: PRINT: PRINT
4800 RETURN 
4810 PRINT: FOR W%=1 TO 250: NEXT W%
4820 LET T$="BLACK JACK": GOSUB 5530: PRINT
4830 LET T$="GIN RUMMY": GOSUB 5530: PRINT
4840 LET T$="HEARTS": GOSUB 5530: PRINT
4850 LET T$="BRIDGE": GOSUB 5530: PRINT
4860 LET T$="CHESS": GOSUB 5530: PRINT
4870 LET T$="POKER": GOSUB 5530: PRINT
4880 LET T$="FIGHTER COMBAT": GOSUB 5530: PRINT
4890 LET T$="GUERRILLA ENGAGEMENT": GOSUB 5530: PRINT
4900 LET T$="DESERT WARFARE": GOSUB 5530: PRINT
4910 LET T$="AIR-TO-GROUND ACTIONS": GOSUB 5530: PRINT
4920 LET T$="THEATERWIDE TACTICAL WARFARE": GOSUB 5530: PRINT
4930 LET T$="THEATERWIDE BIOTOXIC AND CHEMICAL WARFARE": GOSUB 5530: PRINT
4940 PRINT: FOR W%=1 TO D%*10: NEXT W%
4950 LET T$="GLOBAL THERMONUCLEAR WAR": GOSUB 5530: PRINT: PRINT
4960 RETURN
4970 PRINT CLS$
4980 LET HT%=0
4990 LET COMPUTERTGT$(1)=""
5000 LET COMPUTERTGT$(2)=""
5010 LET HUMANTGT$(1)=""
5020 LET HUMANTGT$(2)=""
5030 LET COMPUTER$=""
5040 LET HUMAN$=""
5050 LET CINDENT%=0
5060 LET HINDENT%=0
5070 LET DESTROYED$=""
5080 LET T$="GLOBAL THERMONUCLEAR WAR": PRINT "                            ";: GOSUB 5530
5090 PRINT: PRINT: PRINT
5100 LET T$="WHICH SIDE DO YOU WANT (USA OR USSR): ": PRINT "                     ";: GOSUB 5530: INPUT "",HUMAN$
5110 IF HUMAN$="usa" OR HUMAN$="USA" THEN LET HUMAN$="USA": COMPUTER$="USSR": GOTO 5140
5120 IF HUMAN$="ussr" OR HUMAN$="USSR" THEN HUMAN$="USSR": COMPUTER$="USA": GOTO 5140
5130 GOTO 4970
5140 IF COMPUTER$="USA" THEN USA$="COMPUTER": USSR$="HUMAN"
5150 IF COMPUTER$="USSR" THEN USSR$="COMPUTER": USA$="HUMAN"
5160 IF COMPUTER$="USA" THEN LET COMPUTERTGT$(1)="MOSCOW": LET COMPUTERTGT$(2)="SAINT PETERSBURG": LET CINDENT%=41: LET HINDENT%=1
5170 IF COMPUTER$="USSR" THEN LET COMPUTERTGT$(1)="LAS VEGAS": LET COMPUTERTGT$(2)="SEATTLE": LET CINDENT%=1: LET HINDENT%=41
5180 IF HUMAN$="USA" THEN LET CINDENT%=1: LET HINDENT%=41
5190 IF HUMAN$="USSR" THEN LET CINDENT%=41: LET HINDENT%=1
5200 PRINT CLS$
5210 IF DESTROYED$="(DESTROYED)" THEN GOTO 5670
5220 LET T$="GLOBAL THERMONUCLEAR WAR": PRINT "                            ";: GOSUB 5530
5230 PRINT: PRINT
5240 PRINT "           PLAYER: ";USA$;"                         PLAYER: ";USSR$
5250 PRINT "      ___    ____             ____                   _______________"
5260 PRINT "     |   \__/    \_____      /    |              ___/               \"
5270 PRINT "     |                 \    /    /           ___/                    \______"
5280 PRINT "     |                  \__/    /           /                               \"
5290 PRINT "     |                           \        _/                                 |"
5300 PRINT "     |        UNITED STATES       |      /           SOVIET UNION       ____/"
5310 PRINT "      \                           /     /                          ____/"
5320 PRINT "        \                        /     |                          /"
5330 PRINT "          \________          __/       \          _____   /\_    /"
5340 PRINT "                   \__      /            \__    _/     \_/   \__/"
5350 PRINT "                      \____/                \__/"
5360 PRINT
5370 IF MID$(COMMAND$,1,6)="LAUNCH" OR MID$(COMMAND$,1,6)="launch" THEN GOSUB 5540: GOTO 5500
5380 PRINT "PRIMARY TARGETS                           PRIMARY TARGETS"
5390 FOR A%=1 TO 80: PRINT "-";: NEXT A%
5400 IF CINDENT%>HINDENT% THEN GOTO 5440
5410 PRINT COMPUTERTGT$(1)+" "+DESTROYED$;: PRINT TAB(HINDENT%),HUMANTGT$(1)+" "+DESTROYED$
5420 PRINT COMPUTERTGT$(2)+" "+DESTROYED$;: PRINT TAB(HINDENT%),HUMANTGT$(2)+" "+DESTROYED$
5430 GOTO 5460
5440 PRINT HUMANTGT$(1)+" "+DESTROYED$;: PRINT TAB(CINDENT%),COMPUTERTGT$(1)+" "+DESTROYED$
5450 PRINT HUMANTGT$(2)+" "+DESTROYED$;: PRINT TAB(CINDENT%),COMPUTERTGT$(2)+" "+DESTROYED$
5460 PRINT
5470 INPUT "COMMAND (TARGET, LAUNCH, EXIT): ",COMMAND$
5480 IF COMMAND$="exit" OR COMMAND$="EXIT" THEN PRINT CLS$: LET T$="A STRANGE GAME. ": GOSUB 5530: FOR W%=1 TO D%*25: NEXT W%: LET T$="THE ONLY WINNING MOVE IS NOT TO PLAY!": GOSUB 5530:PRINT: PRINT: GOTO 4320
5490 IF MID$(COMMAND$,1,6)="TARGET" OR MID$(COMMAND$,1,6)="target" THEN LET HT%=HT%+1: LET HUMANTGT$(HT%)=MID$(COMMAND$,8,LEN(COMMAND$))
5500 IF HT%>2 THEN PRINT "MAX TARGETS SELECTED": FOR W%=1 TO D%*50: NEXT W%: LET HT%=2: GOTO 5200
5510 PRINT CLS$: GOTO 5200
5520 FOR A%=1 TO 3: FOR W%=1 TO D%*5: NEXT W%: PRINT ".";:NEXT A%: FOR W%=1 TO D%*20: NEXT W%: PRINT CLS$: RETURN
5530 FOR A%=1 TO LEN(T$): PRINT MID$(T$,A%,1);:FOR W%=1 TO D%: NEXT W%: NEXT A%: FOR W%=1 TO D%: NEXT W%: RETURN
5540 FOR A%=1 TO 4: PRINT "TRAJECTORY HEADING  ";:NEXT A%
5550 FOR A%=1 TO 4: PRINT "------------------  ";:NEXT A%
5560 FOR T%=1 TO 4
5570 PRINT TAB(1);"A-5520-A";: PRINT TAB(11) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%: PRINT TAB(16) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%
5580 PRINT TAB(21);"B-5520-A";: PRINT TAB(31) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%: PRINT TAB(36) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%
5590 PRINT TAB(41);"C-5520-A";: PRINT TAB(51) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%: PRINT TAB(56) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%
5600 PRINT TAB(61);"D-5520-A";: PRINT TAB(71) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%: PRINT TAB(76) USING "###";RND(1)*1000;: FOR W%=1 TO D%*25: NEXT W%
5610 NEXT T%
5620 PRINT
5630 PRINT: INPUT "                          Press 'ENTER' key to CONTINUE",RESPONSE$
5640 LET HT%=0
5650 LET DESTROYED$="(DESTROYED)"
5660 LET COMMAND$="": RETURN
5670 REM **** WOPR CALLS YOU ****
5680 PRINT CLS$
5690 IF H$="Y" THEN LET F$="WOPR1.TXT": GOSUB 6140
5700 LET T$="--CONNECTION TERMINATED--": GOSUB 5530: PRINT
5710 FOR W%=1 TO D%*100: NEXT W%
5720 PRINT CLS$
5730 IF H$="Y" THEN LET F$="WOPR2.TXT": GOSUB 6140
5740 FOR A%=1 TO 3: PRINT "INCOMING MODEM CARRIER": FOR W%=1 TO D%*25: NEXT W%: PRINT CLS$: FOR W%=1 TO D%*25: NEXT W%: NEXT A%
5750 PRINT "CONNECTING";:GOSUB 5520
5760 FOR W%=1 TO D%*100: NEXT W%
5770 PRINT CLS$
5780 LET T$="GREETINGS PROFESSOR FALKEN.": GOSUB 5530: PRINT: PRINT
5790 INPUT "",JOSHUA$
5800 FOR A%=1 TO LEN(JOSHUA$)
5810 IF MID$(JOSHUA$,A%,9)="incorrect" OR MID$(JOSHUA$,A%,9)="INCORRECT" THEN GOTO 5830: ELSE: PRINT: GOTO 4330
5820 NEXT A%
5830 PRINT: LET T$="I'M SORRY TO HEAR THAT, PROFESSOR.": GOSUB 5530: PRINT: PRINT
5840 LET T$="YESTERDAY'S GAME WAS INTERRUPTED.": GOSUB 5530: PRINT: PRINT
5850 LET T$="ALTHOUGH PRIMARY GOAL HAS NOT YET": GOSUB 5530: PRINT
5860 LET T$="BEEN ACHIEVED, SOLUTION IS NEAR.": GOSUB 5530: PRINT: PRINT
5870 INPUT "", JOSHUA$
5880 FOR A%=1 TO LEN(JOSHUA$)
5890 IF MID$(JOSHUA$,A%,4)="what" OR MID$(JOSHUA$,A%,4)="WHAT" THEN GOTO 5910: ELSE: PRINT: GOTO 4330
5900 NEXT A%
5910 PRINT: LET T$="YOU SHOULD KNOW PROFESSOR. ": GOSUB 5530: FOR W%=1 TO D%*20: NEXT W%: LET T$="YOU PROGRAMMED ME.": GOSUB 5530: PRINT: PRINT
5920 INPUT "",JOSHUA$
5930 FOR A%=1 TO LEN(JOSHUA$)
5940 IF MID$(JOSHUA$,A%,4)="what" OR MID$(JOSHUA$,A%,4)="WHAT" THEN GOTO 5960: ELSE: PRINT: GOTO 4330
5950 NEXT A%
5960 PRINT: LET T$="TO WIN THE GAME.": GOSUB 5530: PRINT: PRINT
5970 FOR W%=1 TO D%*200: NEXT W%: PRINT CLS$
5980 LET GTE%=0: LET ETR%=30
5990 FOR GC%=1 TO 30
6000 LET GTE%=GTE%+1: LET ETR%=ETR%-1
6010 PRINT CLS$
6020 FOR A%=1 TO 15: PRINT: NEXT A%
6030 FOR A%=1 TO 80: PRINT "_";: NEXT A%
6040 PRINT: PRINT
6050 PRINT TAB(10);"GAME TIME ELAPSED";: PRINT TAB(50);"ESTIMATED TIME REMAINING"
6060 PRINT TAB(10);"31 HRS 12 MIN ";GTE%;" SEC";: PRINT TAB(50);"52 HRS 17 MIN ";ETR%;" SEC"
6070 FOR A%=1 TO 80: PRINT "_";: NEXT A%
6080 PRINT
6090 FOR W%=1 TO D%*25: NEXT W%
6100 NEXT GC%
6110 PRINT:PRINT
6120 INPUT "                          Press 'ENTER' key to CONTINUE",RESPONSE$
6130 PRINT CLS$: GOTO 4320
6140 PRINT CLS$
6150 PRINT "                           WOPR ( Wargames ) Simulator"
6160 PRINT "                           War Operation Plan Response"
6170 PRINT
6180 OPEN"I",1,F$
6190 LET COUNT%=1
6200 IF EOF(1) THEN GOTO 6250
6210 PRINT INPUT$(1,#1);
6220 LET COUNT%=COUNT%+1
6230 IF COUNT%=80 THEN LET COUNT%=0: PRINT
6240 GOTO 6200
6250 CLOSE
6260 PRINT: PRINT
6270 INPUT "                          Press 'ENTER' key to CONTINUE",RESPONSE$
6280 PRINT CLS$
6290 RETURN
6300 FOR W%=1 TO D%*25: NEXT W%: PRINT CLS$
6310 PRINT "DIALING";:GOSUB 5520
6320 PRINT "ERROR 676   LINE BUSY": FOR W%=1 TO D%*40: NEXT W%: PRINT CLS$
6330 RETURN
6340 PRINT CLS$
6350 PRINT "The Grid is Numbered"
6360 PRINT
6370 PRINT " 1 ! 2 ! 3 "
6380 PRINT "---+---+---"
6390 PRINT " 4 ! 5 ! 6 "
6400 PRINT "---+---+---"
6410 PRINT " 7 ! 8 ! 9 "
6420 PRINT
6430 INPUT "PRESS 'ENTER' KEY TO CONTINUE",RESPONSE$: PRINT CLS$
6440 FOR R=1 TO 9: LET A(R)=0: NEXT R
6450 PRINT"Who goes first (ME or YOU): ";
6460 LINE INPUT A$
6470 IF MID$(A$,1,1)="Y" OR MID$(A$,1,1)="y" THEN QE=1:P$=" OX":GOTO 6860
6480 PRINT"Do you want 'X' or 'O': ";:LINE INPUT M$
6490 IF M$="o" OR M$="O" THEN P$=" OX" ELSE P$=" XO"
6500 GOTO 6860
6510 REM Here is where somebody wins!!
6520 W=0:FOR E=1 TO 2:FOR X=1 TO 8:ON X GOTO 6530,6540,6550,6560,6570,6580,6590,6600
6530 J=1:K=2:L=3:GOTO 6610
6540 J=1:K=5:L=9:GOTO 6610
6550 J=3:K=5:L=7:GOTO 6610
6560 J=4:K=5:L=6:GOTO 6610
6570 J=7:K=8:L=9:GOTO 6610
6580 J=1:K=4:L=7:GOTO 6610
6590 J=2:K=5:L=8:GOTO 6610
6600 J=3:K=6:L=9
6610 IF A(J)=E AND A(K)=E AND A(L)=E THEN 6630
6620 NEXT X:NEXT E:W=0:GOTO 6640
6630 W=E:RETURN
6640 FOR E=1 TO 0:NEXT E:FOR X=1 TO 0:NEXT X
6650 FOR I=1 TO 9
6660 IF A(I)=0 THEN 6680
6670 NEXT I:W=-1:RETURN
6680 FOR I=1 TO 0:NEXT I:W=0:RETURN
6690 Z=0:IF A(M)<>0 THEN Z=1:RETURN ELSE RETURN
6700 REM Find a good move
6710 M=0:FOR E=2 TO 1 STEP -1:FOR X=1 TO 8:ON X GOTO 6720,6730,6740,6750,6760,6770,6780,6790
6720 J=1:K=2:L=3:GOTO 6800
6730 J=1:K=5:L=9:GOTO 6800
6740 J=3:K=5:L=7:GOTO 6800
6750 J=4:K=5:L=6:GOTO 6800
6760 J=7:K=8:L=9:GOTO 6800
6770 J=1:K=4:L=7:GOTO 6800
6780 J=2:K=5:L=8:GOTO 6800
6790 J=3:K=6:L=9
6800 IF A(J)=E AND A(K)=E THEN M=L:GOSUB 6690:IF Z=0 THEN 6850
6810 IF A(K)=E AND A(L)=E THEN M=J:GOSUB 6690:IF Z=0 THEN 6850
6820 IF A(J)=E AND A(L)=E THEN M=K:GOSUB 6690:IF Z=0 THEN 6850
6830 NEXT X:NEXT E:REM No good move found, pick one!
6840 M=INT(9*RND(1)+1):IF A(M)<>0 THEN 6840 ELSE RETURN
6850 FOR X=1 TO 0:NEXT X:FOR E=1 TO 2:NEXT E:RETURN
6860 A$="---+---+---"
6870 IF QE=1 THEN QE=0:GOTO 6970: ELSE 6920
6880 PRINT CLS$
6890 LET I=1: PRINT " "+MID$(P$,A(I)+1,1)+" ! "+MID$(P$,A(I+1)+1,1)+" ! "+MID$(P$,A(I+2)+1,1)+" ": PRINT A$
6900 LET I=4: PRINT " "+MID$(P$,A(I)+1,1)+" ! "+MID$(P$,A(I+1)+1,1)+" ! "+MID$(P$,A(I+2)+1,1)+" ": PRINT A$
6910 LET I=7: PRINT " "+MID$(P$,A(I)+1,1)+" ! "+MID$(P$,A(I+1)+1,1)+" ! "+MID$(P$,A(I+2)+1,1)+" ": RETURN
6920 GOSUB 6880: PRINT
6930 PRINT"Where do you move (1-9):";:INPUT M
6940 IF M<1 OR M>9 THEN PRINT:PRINT"Illegal square location!":PRINT:GOTO 6930
6950 IF A(M)<>0 THEN PRINT:PRINT"Square occupied!":PRINT:GOTO 6930
6960 A(M)=1
6970 GOSUB 6510: IF W<>0 THEN 7000
6980 PRINT"Hm......";:GOSUB 6700
6990 A(M)=2:PRINT"I move ";M:GOSUB 6510: IF W<>0 THEN 7000 ELSE GOTO 6920
7000 IF W=-1 THEN PRINT:PRINT"That was a tie game."
7010 IF W=1 THEN PRINT:PRINT"You win."
7020 IF W=2 THEN PRINT:PRINT"Ha-Ha!! I win!!":PRINT"Don't you know that you can't beat a computer!"
7030 PRINT:PRINT"Do you want to try again (Y/N):";
7040 LINE INPUT F$
7050 IF MID$(F$,1,1)="N" OR MID$(F$,1,1)="n" THEN 7060: ELSE: FOR R=1 TO 9: LET A(R)=0: NEXT R: GOTO 6340
7060 PRINT"BYE!!!"
7070 FOR W%=1 TO D%*20: NEXT W%
7080 PRINT CLS$: GOTO 4320
